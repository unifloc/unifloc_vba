'=======================================================================================
'Unifloc 7.7  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
' функции расчета скважины для проведения расчетов из интерфейса Excel
Option Explicit
' ==============  функции для расчета скважины ==========================
' =====================================================================
Private Function wellGL_InitData(Q_m3Day As Double, _
                                 fw_perc As Double, _
                        Optional Pcas_atma As Double = 10, _
                        Optional wellStr As String = WELL_GL_DEFAULT, _
                        Optional PVTstr As String = PVT_DEFAULT, _
                        Optional HydrCorr As H_CORRELATION = 0 _
                                 ) As CWellGL
    ' функция для шаблонного чтения данных по скважине в интерфейсных функциях
    '
    ' на входе данные по конструкции скважины, PVT, ГЛ берутся из строк с закодированными параметрами
    '    следует использовать функции Encode для передачи параметров
    ' на выходе объект скважина с загруженными данными
    Dim well As New CWellGL
    Dim PVT As New CPVT
        
    Set PVT = PVT_decode_string(PVTstr)
    PVT.Qliq_scm3day = Q_m3Day
    PVT.wc_fr = fw_perc / 100
    
    Set well = wellGL_decode_string(wellStr)
    Set well.Fluid = PVT
  '  well.Pcas_atma = Pcas_atma
    well.HFlowCorrelation = HydrCorr
    Set wellGL_InitData = well
End Function
Private Function wellGL_out_arr(well As CWellGL, Optional FirsrCol As Integer = 0)
    Dim ar1, ar2
    With well
        ' подготовим данные для вывода
        ' данные выводятся в одну линию, чтобы, хотя бы теоретически, можно было вывести данные по скважине в таблице
        ' во второй строке выводятся подписи параметров, если необходимо
        ReDim ar1(20)
        ReDim ar2(20)
        Dim i As Integer
        ' первый параметр настраиваемый
        i = 0
        ar1(0) = "":  ar2(0) = ""
        ' блок параметров по давлениям
        i = i + 1
        ar1(i) = .Pline_atma:  ar2(i) = "Pline_atma"
        i = i + 1
        ar1(i) = .Pbuf_atma:  ar2(i) = "Pbuf_atma"
        i = i + 1
        ar1(i) = .Pcas_atma:  ar2(i) = "Pcas_atma"
        i = i + 1
        ar1(i) = .Pgas_inj_atma:   ar2(i) = "Pgas_inj_atma"
        i = i + 1
        ar1(i) = .Pwf_atma:  ar2(i) = "Pwf_atma"
        ' параметры температуры
        i = i + 1
        ar1(i) = .Tbuf_C:  ar2(i) = "Tbuf_C"
        i = i + 1
        ar1(i) = .tbh_C:  ar2(i) = "tbh_C"
        
        Select Case FirsrCol
            Case 0
                ar1(0) = .Pline_atma: ar2(0) = "Plin"
            Case 1
                ar1(0) = .Pwf_atma:   ar2(0) = "Pwf"
        End Select
        
        wellGL_out_arr = Array(ar1, ar2)
    ' можно еще добавить сюда вывод кривых распределения давления и температуры по стволу и еще 4 параметров  (потом)
    End With
End Function
Public Function wellGL_Plin_Pwf_atma(ByVal Pwf_atma As Double, _
                                     ByVal Q_m3Day As Double, _
                                     ByVal fw_perc As Double, _
                                     Optional ByVal Pcas_atma As Double = 10, _
                                     Optional Qgas_inj_scm3day As Double = -1, _
                                     Optional wellStr As String = WELL_GL_DEFAULT, _
                                     Optional PVTstr As String = PVT_DEFAULT, _
                                     Optional ESPstr As String = ESP_DEFAULT, _
                                     Optional ByVal HydrCorr As H_CORRELATION = 0)
' функция расчета линейного давления скважины по забойному
    Dim well As CWellGL
    Set well = wellGL_InitData(Q_m3Day, fw_perc, Pcas_atma, _
                               wellStr, PVTstr, AnsariCor)
    Call well.SetQgasInj(Pcas_atma, Qgas_inj_scm3day)
    Call well.Calc_Plin_Pwf_atma_(Pwf_atma)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    wellGL_Plin_Pwf_atma = wellGL_out_arr(well, 0)
    
End Function
Public Function wellGL_Pwf_Plin_atma(ByVal Plin_atma As Double, _
                                     ByVal Q_m3Day As Double, _
                                     ByVal fw_perc As Double, _
                                     Optional ByVal Pcas_atma As Double = 10, _
                                     Optional Qgas_inj_scm3day As Double = -1, _
                                     Optional wellStr As String = WELL_GL_DEFAULT, _
                                     Optional PVTstr As String = PVT_DEFAULT, _
                                     Optional ESPstr As String = ESP_DEFAULT, _
                                     Optional ByVal HydrCorr As H_CORRELATION = 0)
' функция расчета линейного давления скважины по забойному
    Dim well As CWellGL
    Set well = wellGL_InitData(Q_m3Day, fw_perc, Pcas_atma, _
                               wellStr, PVTstr, AnsariCor)
    Call well.SetQgasInj(Pcas_atma, Qgas_inj_scm3day)
    Call well.calc_Pwf_Plin_atma(Plin_atma, well.tbh_C)            ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    wellGL_Pwf_Plin_atma = wellGL_out_arr(well, 0)
    
End Function
