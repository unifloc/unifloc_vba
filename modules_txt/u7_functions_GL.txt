''=======================================================================================
''Unifloc 7.12  Apodemus agrarius                                           khabibullin.ra@gubkin.ru
''Petroleum engineering calculations modules (macroses)
''2000 - 2019
''
''=======================================================================================
Option Explicit
Function GL_dPgasPipe_atmg(ByVal h_m As Double, ByVal P_atmg As Double, ByVal t_C As Double, _
                               Optional ByVal d_cas_mm As Double = 125, _
                               Optional ByVal dtub_mm As Double = 73, _
                               Optional ByVal gamma_gas As Double = 0.8, _
                               Optional ByVal q_gas_scm3day As Double = 10000, _
                               Optional ByVal roughness As Double = 0.001, _
                               Optional ByVal THETA As Double = 90 _
                               ) As Double
  
'de - external diameter, m
'di - interior diameter, m
'gamma_gas - relative density of gas
'qg_sc - gas flow, m3/d
'eps - pipe roughness, m
'theta - ,degree
'length - pipe length, m
'T - temperature, C
'P - pressure, atma
On Error GoTo err1:
Dim de, Di, qg_sc, eps, length, t, P
de = d_cas_mm / 1000
Di = dtub_mm / 1000
qg_sc = q_gas_scm3day
eps = roughness
length = h_m
t = t_C
P = P_atmg
    
    'convert m3/d to scf/d
    qg_sc = qg_sc * 3.28 ^ 3
    
    Dim P_MPa As Double, P_psi As Double
    P_MPa = P * 0.1013 'convert atma to Mpa
    P_psi = P * 14.696 ' convert atma to psi
    
    
    Dim T_K As Double, t_F As Double
    T_K = t + 273 'convert Celcsius to Kelvin
    t_F = (9 / 5) * t + 32 'convert Celcsius to Fahrengheit
     
    Dim T_pc As Double
    Dim p_pc As Double
    Dim z As Double
    
'        T_pc = PseudoTemperatureStanding(gamma_gas)
'        p_pc = PseudoPressureStanding(gamma_gas)
'        Z = ZFactorDranchuk(T_K / T_pc, P_MPa / p_pc)
    z = unf_calc_Zgas_d(T_K, P_MPa, gamma_gas)
    
    eps = eps * 39.3701 'convert m to in
    
    Dim de_in As Double, di_in As Double
    di_in = Di * 39.3701 'convert m to in
    de_in = de * 39.3701 'convert m to in
    Dim dh As Double, DA As Double, deq As Double
    dh = de_in - di_in
    DA = (de_in ^ 2 - di_in ^ 2) ^ 0.5
    If di_in = 0 Then
        deq = de_in
    Else
        deq = (de_in ^ 2 + di_in ^ 2 - (de_in ^ 2 - di_in ^ 2) / Log(de_in / di_in)) / (de_in - di_in)
    End If
    Dim mu_g As Double
    mu_g = unf_calc_mu_gas_cP(T_K, P_MPa, z, gamma_gas)
    Dim Re As Double
    Re = 0.020107 * gamma_gas * Abs(qg_sc) * deq / mu_g / DA ^ 2
    Dim a As Double, b As Double
    a = (2.457 * Log(1 / ((7 / Re) ^ 0.9 + 0.27 * eps / deq))) ^ 16
    b = (37530 / Re) ^ 16
    Dim f_moody As Double
    f_moody = 8 * ((8 / Re) ^ 12 + 1 / ((a + b) ^ 1.5)) ^ (1 / 12)
    
    Dim gradP As Double
    
    gradP = -0.018786 * gamma_gas * (P_psi + 14.7) * Sin(THETA * Application.Pi / 180) / (t_F + 460) / z + (1.2595 * 10 ^ (-11)) * f_moody * (t_F + 460) * z * gamma_gas * (qg_sc ^ 2) / (P_psi + 14.7) / dh / DA ^ 4
    gradP = gradP * 0.068 / 0.3048 'convert psi/ft to atma/m
    
    GL_dPgasPipe_atmg = P + gradP * length
    
    Exit Function
err1:
    GL_dPgasPipe_atmg = -1
    addLogMsg "error in function : GL_dPgasPipe_atmg"
    
End Function
' функция для поиска решения по расчету давления в клапане
Public Function calc_dq_gas_pu_valve(Pu As Double, coeffA)
   Dim q_gas As Double, d_mm As Double, pd As Double, gg As Double, t As Double
   q_gas = coeffA(0)
   d_mm = coeffA(1)
   pd = coeffA(2)
   gg = coeffA(3)
   t = coeffA(4)
   
   calc_dq_gas_pu_valve = q_gas - GLV_q_gas_sm3day(d_mm, Pu, pd, gg, t)(0)(0)
End Function
Public Function calc_dq_gas_pd_valve(pd As Double, coeffA)
   Dim q_gas As Double, d_mm As Double, Pu As Double, gg As Double, t As Double
   q_gas = coeffA(0)
   d_mm = coeffA(1)
   Pu = coeffA(2)
   gg = coeffA(3)
   t = coeffA(4)
   calc_dq_gas_pd_valve = q_gas - GLV_q_gas_sm3day(d_mm, Pu, pd, gg, t)(0)(0)
End Function
' функция расчета ошибки в расходе газа при произвольном давлении внутри клапана
Public Function calc_dq_gas_pv_vkr_valve(Pv As Double, coeffA)
   Dim q_gas As Double, d_mm As Double, Pu As Double, pd As Double, gg As Double, t As Double
   Dim d_vkr As Double
   
   d_mm = coeffA(0)
   d_vkr = coeffA(1)
   Pu = coeffA(2)
   pd = coeffA(3)
   gg = coeffA(4)
   t = coeffA(5)
   
   calc_dq_gas_pv_vkr_valve = GLV_q_gas_sm3day(d_mm, Pu, Pv, gg, t)(0)(0) - GLV_q_gas_sm3day(d_vkr, Pv, pd, gg, t)(0)(0)
End Function

