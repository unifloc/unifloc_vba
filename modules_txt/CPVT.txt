'=======================================================================================
'Unifloc 7.10  Apodemus agrarius                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'class_description_to_manual      - для автогенерации описания - помещает комментарии в мануал (со след строки)
' Класс CPVT описывает свойства пластовых флюидов - нефти, газа и воды
' на основе модели нелетучей нефти (black oil), а также параметры потока
' флюидов - дебита $q_{liq}$ и обводненности $f_w$.
' Позволяет для заданных термобарических условий определить все
' необходимые для проведения расчетов параметры потока, такие как
' газосодержание в нефти, долю свободного газа в потоке,
' плотности и вязкости флюидов, объемный расход флюидов и смеси.
'description_end
Option Explicit
 Private p_clname As String                         ' имя класса для унифицированной генерации сообщений об ошибках
 ' рабочие условия для которых проводится расчет
 Private p_PTcalc As PTtype                         ' термобарические условия при которых был проведен расчет
 ' базовые параметры флюида
 Private p_gamma_o As Double                        ' плотность нефти удельная
 Private p_gamma_g As Double                        ' плотность газа удельная
 Private p_gamma_w As Double                        ' плотность воды удельная
 Private p_rsb_m3m3 As Double                       ' газосодержание при давлении насыщения
 ' калибровочные параметры нефти
 Private p_pb_atma As Double                         ' давление насыщения  (калибровочное значение)
 Private p_muob_cP As Double                       ' вязкость нефти при давлении насыщения (калибровочное значение)
 Private p_bob_m3m3 As Double                       ' объемный коэффициент при давлении насыщения
 Private p_tres_C As Double                         ' пластовая температура при которой заданые значения давления насыщения и объемного коэффициента
 ' расчетные параметры нефти
 Private p_pbcalc_atma As Double                     ' расчетное значение давления насыщения по корреляции
 Private p_rs_m3m3 As Double                        ' расчетное значение газосодержания в нефти при текущих условиях
 Private p_bo_m3m3 As Double                        ' объемный коэффициент нефти при рабочих условиях
 Private p_mu_oil_cP As Double                         ' вязкость нефти при рабочих условиях
 Private p_mu_deadoil_cP As Double                 ' вязкость дегазированной нефти
 Private p_copmressibility_o_1atm As Double         ' сжимаемость нефти
' Private p_sigma_oil_gas_Nm As Double                     ' поверхностное натяжение
' Private p_sigma_wat_gas_Nm As Double                     ' поверхностное натяжение
 Private p_ST_oilgas_dyncm As Double                ' поверхностное натяжение нефть газ
 Private p_ST_watgas_dyncm As Double                ' поверхностное натяжение вода газ
 Private p_ST_liqgas_dyncm As Double
 ' расчетные параметры газа
 Private p_Z As Double                              ' расчетное значение коэффициента сверхсжимаемости
 Private p_bg_m3m3 As Double                        ' объемный коэффициент газа
 Private p_mu_gas_cP As Double                         ' вязкость газа при рабочих условиях
 ' расчетные параметры воды
 Private p_bw_m3m3 As Double                        ' расчетное значение объемного коэффициента воды
 Private p_bwSC_m3m3 As Double
 Private p_mu_wat_cP As Double                         ' вязкость воды
 Private p_salinity_ppm As Double                   ' соленость воды
 ' параметры потока
 Private p_rp_m3m3 As Double                        ' газовый фактор добычной (приведенный к стандартным условиям)
 Private p_fw_fr As Double                          ' объемная доля воды в флюиде
 Private p_qliq_sm3day As Double                   ' задаем для флюида также и дебиты, это упрощает дальнейшие расчеты расходов в разных условиях
 Private p_q_oil_rc_m3day As Double
 Private p_q_wat_rc_m3day As Double
 Private p_q_gas_rc_m3day As Double
 Private p_q_liq_rc_m3day As Double
 Private p_gas_fraction_d As Double
 Private p_mu_mix_cP As Double
 Private p_rho_oil_rc_kgm3 As Double
 Private p_rho_wat_rc_kgm3 As Double
 Private p_rho_liq_rc_kgm3 As Double
 Private p_rho_mix_rc_kgm3 As Double
 Private p_q_gas_free_sm3day As Double                 ' дебит газа, вернее добавка для значения Qgas, в исследовательских целях
 ' набор параметров для температурных расчетов
 Private p_co_JkgC As Double                        ' oil heat capacity   теплоемкость нефти  Дж/кг/С
 Private p_cw_JkgC As Double                        ' water heat capacity  теплоемкость воды
 Private p_cg_JkgC As Double                        ' теплоемкость газа gas heat capacity
 ' настройка модели
 Private p_PVT_correlation As PVT_CORRELATION       ' PVT correlation
 Public zCorr As Z_CORRELATION
' Public calculated As Boolean
 Private p_ZNLF As Boolean                          ' флаг показывает что флюид в режиме барботажа
 Private p_twh_C As Double                          ' температура для которой строятся кривые
                                                    '  в режиме барботажа газ вспплывает через неподвижный столб жидкости (нефти )
                                                    '  задается дополнительно расход свободного газа через столб жидкости
                                                    '  при этом газ не выделяется из жидкости
                                                    '  корректируется обводненность - в неподвижном потоке может остаться только нефть
' Private c_RsTres_curve As New CInterpolation       ' кривая газосодержания от давления при пластовой температуре
' Private c_RsT_curve As New CInterpolation          ' кривая газосодержания от давления при произвольной температуре
' Private c_BoTres_curve As New CInterpolation       ' кривая объемного коэффициента нефти от давления при пластовой температуре
' Private c_BoT_curve As New CInterpolation          ' кривая объемного коэффициента нефтиот давления при пластовой температуре
' Private c_MuoTres_curve As New CInterpolation      ' кривая вязкости нефти от давления при пластовой температуре
' Private c_MuoT_curve As New CInterpolation         ' кривая вязкости нефти от давления при пластовой температуре
' Private c_MugTres_curve As New CInterpolation      ' кривая вязкости газа от давления при пластовой температуре
' Private c_MugT_curve As New CInterpolation         ' кривая вязкости газа от давления при пластовой температуре
 Public gas_only As Boolean
 Public Property Get ZNLF() As Boolean
    ZNLF = p_ZNLF
 End Property
  
 Public Property Let ZNLF(ByVal val As Boolean)
    p_ZNLF = val
    If p_ZNLF Then
        fw_fr = 0      ' обнуляем содержание воды
        qliq_sm3day = const_ZNLF_rate ' зануляем поток жидкости - ничего не движется. Это занулит поток газа из скважины
    Else
        ' ничего не делаем
    End If
 End Property
  
 Public Property Get fw_fr() As Double
   fw_fr = p_fw_fr
 End Property
  
 Public Property Let fw_fr(val As Double)
    If val >= 0 And val <= 1 Then
        p_fw_fr = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "fw_fr"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
 Public Property Get fw_perc() As Double
   fw_perc = p_fw_fr * 100
 End Property
  
 Public Property Let fw_perc(val As Double)
   fw_fr = val / 100    ' вызываем свойство, чтобы поменять все зависимые флюиды
 End Property
 
  
 Public Property Get qliq_sm3day() As Double
    qliq_sm3day = p_qliq_sm3day
 End Property
 
 Public Property Let qliq_sm3day(val As Double)
    If val >= 0 And val <= 100000 Then
        ' not ready for too high liquid rates here - 100000
        p_qliq_sm3day = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "qliq_sm3day"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
  
 Private Sub calc_wc()
    If qliq_sm3day > 0 Then
        fw_fr = q_wat_sm3day / qliq_sm3day
    Else
        fw_fr = 0
    End If
 End Sub
 
 Public Property Get q_wat_sm3day() As Double
   q_wat_sm3day = p_qliq_sm3day * p_fw_fr
 End Property
  
 Public Property Get q_gas_sm3day() As Double
   q_gas_sm3day = q_oil_sm3day * rp_m3m3 + q_gas_free_sm3day    ' учитываем наличие свободного газа в потоке
   ' при барботаже будет все корректно
 End Property
 Public Property Get q_gas_insitu_sm3day() As Double
 ' расход газа в заданных термобарических условиях приведенный к стандартным условиям
   q_gas_insitu_sm3day = (q_gas_sm3day - rs_m3m3 * q_oil_sm3day)
   If q_gas_insitu_sm3day < 0 Then
        addLogMsg "CPVT.q_gas_insitu_sm3day: warning: q_gas_insitu_sm3day < 0,  q_gas_sm3day = " & S(q_gas_sm3day) & _
                  ", rs_m3m3 = " & S(rs_m3m3) & _
                  ", q_oil_sm3day = " & S(q_oil_sm3day) & _
                  "  q_gas_insitu_sm3day set to 0"
        q_gas_insitu_sm3day = 0
   End If
   ' при барботаже будет все как надо - свободный газ уже учтен в дебите газа в стандартных условиях
 End Property
 Public Property Get q_gas_rc_m3day() As Double
   q_gas_rc_m3day = q_gas_insitu_sm3day * p_bg_m3m3
   If q_gas_rc_m3day < 0 Then q_gas_rc_m3day = 0
 End Property
' Public Property Get q_oil_m3day() As Double
'   q_oil_m3day = q_oil_sm3day * bo_m3m3
' End Property
 
 Public Property Get q_oil_sm3day() As Double
   q_oil_sm3day = p_qliq_sm3day * (1 - p_fw_fr)
 End Property
 
 Public Property Get q_oil_rc_m3day() As Double
   q_oil_rc_m3day = q_oil_sm3day * p_bo_m3m3
 End Property
 Public Property Get q_wat_rc_m3day() As Double
   q_wat_rc_m3day = q_wat_sm3day * p_bw_m3m3
   ' при барботаже будет все как надо
 End Property
 
 Public Property Get q_liq_rc_m3day() As Double
   q_liq_rc_m3day = q_wat_rc_m3day + q_oil_rc_m3day
   ' при барботаже будет все как надо
 End Property
   
 Public Property Get q_mix_rc_m3day() As Double
   q_mix_rc_m3day = p_q_wat_rc_m3day + p_q_oil_rc_m3day + p_q_gas_rc_m3day
   ' при барботаже будет все как надо
 End Property
 Public Property Get q_gas_free_sm3day() As Double
   q_gas_free_sm3day = p_q_gas_free_sm3day
 End Property
 Public Property Let q_gas_free_sm3day(ByVal val As Double)
    If val >= 0 And val <= 1000000000 Then
        ' not ready for too high liquid rates here - 1 000 000 000
        p_q_gas_free_sm3day = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "q_gas_free_sm3day"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
  
 Public Property Get wm_kgsec() As Double
   wm_kgsec = wg_kgsec + wo_kgsec + wwat_kgsec
   ' поскольку для общего массового расхода не обязательно знать расходы при заданных термобарических условий здесь идет расчет без отсылки к фазным
 End Property
  
 Public Property Get compressibility_oil_1atm() As Double
    compressibility_oil_1atm = p_copmressibility_o_1atm
 End Property
 
  
 Public Property Get co_JkgC() As Double  ' oil heat capacity   теплоемкость нефти  Дж/кг/С
    co_JkgC = p_co_JkgC
 End Property
 
 Public Property Get cw_JkgC() As Double   ' water heat capacity  теплоемкость воды
    cw_JkgC = p_cw_JkgC
 End Property
 
 Public Property Get cg_JkgC() As Double  ' теплоемкость газа gas heat capacity
    cg_JkgC = p_cg_JkgC
 End Property
 ' массовый расход газа при заданных термобарических условиях
 Public Property Get wg_kgsec() As Double
    wg_kgsec = rho_gas_rc_kgm3 * q_gas_rc_m3day * const_conver_sec_day
 End Property
 ' массовый расход нефти при заданных термобарических условиях
 Public Property Get wo_kgsec() As Double
    wo_kgsec = rho_oil_rc_kgm3 * q_oil_rc_m3day * const_conver_sec_day
 End Property
 
 ' массовый расход воды при заданных термобарических условиях
 Public Property Get wwat_kgsec() As Double
    wwat_kgsec = rho_wat_rc_kgm3 * q_wat_rc_m3day * const_conver_sec_day
 End Property
 
 ' массовый расход жидкости при заданных термобарических условиях
 Public Property Get wliq_kgsec() As Double
    wliq_kgsec = wo_kgsec + wwat_kgsec * const_conver_sec_day
 End Property
 
 Public Property Get cliq_JkgC() As Double  ' mixture heat capacity   теплоемкость жидкости  Дж/кг/С
    If q_mix_rc_m3day > 0 Then
         cliq_JkgC = (co_JkgC * wo_kgsec + cw_JkgC * wwat_kgsec) / (wwat_kgsec + wo_kgsec)
    Else
        cliq_JkgC = co_JkgC
    End If
 End Property
 Public Property Get cmix_JkgC() As Double  ' mixture heat capacity   теплоемкость жидкости  Дж/кг/С
    If q_mix_rc_m3day > 0 Then
         cmix_JkgC = (cliq_JkgC * wliq_kgsec + cg_JkgC * wg_kgsec) / (wliq_kgsec + wg_kgsec)
    Else
        cmix_JkgC = co_JkgC
    End If
 End Property
 
 Public Property Get cJT_Katm() As Double
 ' коэффциент Джоуля Томсона для многофазной смеси
    Dim X As Double
    Dim wm As Double
    Dim Z1 As Double, Z2 As Double
    Dim dTz As Double
    Dim dZdT As Double
    Dim TZdZdT As Double
    wm = wm_kgsec
    ' здесь необходима производная по сверхсжимаемости, здесь ее и считаем, чтобы ускорить расчет немного
    dTz = 5
      ' расчет производной увеличивает время счета довольно сильно - пока отключен, надо решить, что делать дальше
    Z1 = unf_calc_Zgas_d(tcalc_K, pcalc_MPaa, p_gamma_g)
    Z2 = unf_calc_Zgas_d(tcalc_K + dTz, pcalc_MPaa, p_gamma_g)
    p_Z = Z1
    dZdT = (Z2 - Z1) / dTz
    TZdZdT = tcalc_K / p_Z * dZdT
    If wm > 0 Then
        X = wg_kgsec / wm_kgsec    ' массовая доля газа в потоке
    Else
        X = 0
    End If
    cJT_Katm = 1 / cmix_JkgC * (X / rho_gas_rc_kgm3 * (-TZdZdT) + (1 - X) / rho_liq_rc_kgm3) * const_convert_atma_Pa
 End Property
 
 Public Property Get oil_API() As Double
    oil_API = 141.5 / p_gamma_o - 131.5
 End Property
 
 Public Property Get rho_oil_rc_kgm3() As Double
    Dim msg As String
    If p_bo_m3m3 > 0 Then
        rho_oil_rc_kgm3 = 1000 * (p_gamma_o + p_rs_m3m3 * p_gamma_g * const_rho_air / 1000) / p_bo_m3m3
    Else
        ' странная обработка ошибок не при ввода а при расчета - потом надо будет убрать наверное
        msg = "CPVT.rho_oil_rc_kgm3: расчет плотности с неположительным значением Bo_m3m3" & bo_m3m3 & "Значение Bo проигнорировано"
        addLogMsg msg
        rho_oil_rc_kgm3 = 1000 * (p_gamma_o + p_rs_m3m3 * p_gamma_g * const_rho_air / 1000)
        'Err.Raise kErrPVTinput, , msg
    End If
 End Property
 
 Public Property Get rho_wat_rc_kgm3() As Double
    Dim msg As String
    If p_bw_m3m3 > 0 Then
        rho_wat_rc_kgm3 = 1000 * (p_gamma_w) / p_bw_m3m3
    Else
        ' странная обработка ошибок не при ввода а при расчета - потом надо будет убрать наверное
        msg = "CPVT.rho_wat_rc_kgm3: расчет плотности с неположительным значением Bw_m3m3" & p_bw_m3m3 & "Значение Bw проигнорировано"
        addLogMsg msg
        rho_wat_rc_kgm3 = 1000 * (gamma_w)
       ' Err.Raise kErrPVTinput, , msg
    End If
 End Property
 Public Property Get rho_liq_rc_kgm3() As Double
    rho_liq_rc_kgm3 = p_rho_liq_rc_kgm3 '(1 - fw_fr) * rho_oil_rc_kgm3 + fw_fr * rho_wat_rc_kgm3
 End Property
 
 Public Property Get rho_gas_rc_kgm3() As Double
    Dim msg As String
    If p_bg_m3m3 > 0 Then
        rho_gas_rc_kgm3 = p_gamma_g * const_rho_air / p_bg_m3m3
    Else
        ' странная обработка ошибок не при ввода а при расчета - потом надо будет убрать наверное
        msg = "CPVT.rho_gas_rc_kgm3: расчет плотности с неположительным значением Bg_m3m3" & bg_m3m3 & "Значение Bg проигнорировано"
        addLogMsg msg
        rho_gas_rc_kgm3 = gamma_g * const_rho_air
       ' Err.Raise kErrPVTinput, , msg
    End If
 End Property
 Public Property Get f_g() As Double
    If q_mix_rc_m3day > 0 Then
       f_g = q_gas_rc_m3day / q_mix_rc_m3day
    Else
       f_g = 0
    End If
 End Property
 Public Property Get rho_mix_rc_kgm3() As Double
    rho_mix_rc_kgm3 = p_rho_mix_rc_kgm3  ' rho_liq_rc_kgm3 * (1 - f_g) + rho_gas_rc_kgm3 * f_g
 End Property
 
 Public Property Get sigma_liq_Nm() As Double
    'sigma_liq_Nm = p_sigma_oil_gas_Nm * (1 - fw_fr) + p_sigma_wat_gas_Nm * fw_fr
    sigma_liq_Nm = p_ST_liqgas_dyncm * 0.001
 End Property
 Public Property Get sigma_oil_gas_Nm() As Double
    'sigma_oil_gas_Nm = p_sigma_oil_gas_Nm
    sigma_oil_gas_Nm = p_ST_oilgas_dyncm * 0.001
 End Property
 
 Public Property Let sigma_oil_gas_Nm(val As Double)
    If val >= 0 And val <= 0.5 Then
        ' поверхностное натяжение ртути и воздуха 486 e-3 H/м - вряд ли где то будет больше
        p_ST_oilgas_dyncm = val / 0.001
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "sigma_oil_gas_Nm"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
 
 Public Property Get sigma_wat_gas_Nm() As Double
    'sigma_wat_gas_Nm = p_sigma_wat_gas_Nm
    sigma_wat_gas_Nm = p_ST_watgas_dyncm * 0.001
 End Property
 
 Public Property Let sigma_wat_gas_Nm(val As Double)
    If val >= 0 And val <= 0.5 Then
        ' поверхностное натяжение ртути и воздуха 486 e-3 H/м - вряд ли где то будет больше
        p_ST_watgas_dyncm = val / 0.001
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "sigma_wat_gas_Nm"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
  
 Public Property Get tres_C() As Double
    tres_C = p_tres_C
 End Property
 
 Public Property Get tres_K() As Double
    tres_K = p_tres_C + const_t_K_min
 End Property
 
 Public Property Let tres_C(val As Double)
    If val >= 0 And val <= 300 Then
        ' больше 300 С в пласте не ждем
        p_tres_C = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "tres_C"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
  
 ' молярная масса газа   (используется например в штуцере)
 Public Property Get m_g_kgmol() As Double
    m_g_kgmol = const_m_a_kgmol * p_gamma_g
 End Property
 Public Property Get sal_ppm() As Double
    sal_ppm = p_salinity_ppm
 End Property
 
 Public Property Let sal_ppm(val As Double)
    p_salinity_ppm = val
 End Property
 
 Public Property Get PVT_CORRELATION() As Integer
    PVT_CORRELATION = p_PVT_correlation
 End Property
 
 Public Property Let PVT_CORRELATION(val As Integer)
   p_PVT_correlation = val
 End Property
 ' ----- gamma_o ----------------------------------------------------------------------------------------
 Public Property Get gamma_o() As Double
    gamma_o = p_gamma_o
 End Property
 
 Public Property Get rho_oil_sckgm3() As Double
    rho_oil_sckgm3 = p_gamma_o * const_rho_ref
 End Property
 
 Public Property Get rho_gas_sckgm3() As Double
    rho_gas_sckgm3 = p_gamma_g * const_rho_air
 End Property
 Public Property Get rho_wat_sckgm3() As Double
    rho_wat_sckgm3 = p_gamma_w * const_rho_ref
 End Property
 
 Public Property Let gamma_o(val As Double)
   
    If (val > const_gamma_oil_min) And (val < const_gamma_oil_max) Then
        p_gamma_o = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "gamma_o"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
 
 ' ----- gamma_g ----------------------------------------------------------------------------------------
 Public Property Get gamma_g() As Double
    gamma_g = p_gamma_g
 End Property
 
 Public Property Let gamma_g(val As Double)
 'установка значения плотности газа, влияет на все зависимые флюиды
    
    If (val > const_gamma_gas_min) And (val < const_gamma_gas_max) Then
        p_gamma_g = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "gamma_g"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
 
 ' ----- gamma_w ----------------------------------------------------------------------------------------
 Public Property Get gamma_w() As Double
    gamma_w = p_gamma_w
 End Property
 
 Public Property Let gamma_w(val As Double)
    
    If (val > const_gamma_water_min) And (val < const_gamma_water_max) Then
        p_gamma_w = val
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "gamma_w"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
 End Property
 
' ----- Rp - GOR  ----------------------------------------------------------------------------------------
Public Property Get rp_m3m3() As Double
    rp_m3m3 = p_rp_m3m3
End Property
Property Let rp_m3m3(val As Double)
    If (val >= 0) Then
        p_rp_m3m3 = val
        If p_rp_m3m3 < p_rsb_m3m3 Then   ' проверим, что газовый фактор должен быть больше чем газосодержание
            addLogMsg "Газовый фактор при вводе больше газосодержания Rp = " & Format(p_rp_m3m3, "####0.00") & " < rsb = " & Format(p_rsb_m3m3, "#0.00") & ". Газосодержание исправлено"
            p_rsb_m3m3 = p_rp_m3m3
        End If
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "rp_m3m3"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
End Property
  
' ----- rsb -----------------------------------------------------------------------------------------
Public Property Get rsb_m3m3() As Double
    rsb_m3m3 = p_rsb_m3m3
End Property
Property Let rsb_m3m3(val As Double)
    If (val >= 0) Then
        p_rsb_m3m3 = val
        If p_rp_m3m3 < p_rsb_m3m3 Then   ' проверим, что газовый фактор должен быть больше чем газосодержание
            addLogMsg "газосодержания при вводе меньше газового фактора  Rp = " & Format(p_rp_m3m3, "#0.00") & " < rsb = " & Format(p_rsb_m3m3, "#0.00") & ". Газосодержание исправлено"
            p_rsb_m3m3 = p_rp_m3m3
        End If
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "rsb_m3m3"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
End Property
 
Public Function SetRpRsb(ByVal Rpval_m3m3 As Double, ByVal Rsbval_m3m3 As Double) As Boolean
' безопасный с точки зрения начисления штрафов способ установки произвольных значений газового фактора в системе
    If Rpval_m3m3 > 0 Then
        If Rpval_m3m3 >= Rsbval_m3m3 Then
            p_rp_m3m3 = Rpval_m3m3
            If Rsbval_m3m3 > 0 Then
                p_rsb_m3m3 = Rsbval_m3m3
            Else
                p_rsb_m3m3 = Rpval_m3m3
            End If
            SetRpRsb = True
        Else
            addLogMsg "CPVT.SetRpRsb: Газосодержание при вводе больше газового фактора  Rp = " & Format(Rpval_m3m3, "#0.00") & " < rsb = " & Format(Rsbval_m3m3, "#0.00") & ". Газосодержание исправлено"
            p_rp_m3m3 = Rpval_m3m3
            p_rsb_m3m3 = Rpval_m3m3
            SetRpRsb = True
        End If
    Else
        If Rpval_m3m3 <= 0 And Rsbval_m3m3 > 0 Then
            p_rp_m3m3 = Rsbval_m3m3
            p_rsb_m3m3 = Rsbval_m3m3
            SetRpRsb = True
        Else
            SetRpRsb = False
        End If
    End If
    ' устновим все значения для зависимых флюидов
    rp_m3m3 = rp_m3m3
    rsb_m3m3 = rsb_m3m3
End Function
'----- Pb -----------------------------------------------------------------------------------------
Public Property Get pb_atma() As Double
    If p_pb_atma > 0 Then       ' ноль не допустим, это значит что значение отсутствует
    ' если известно калибровочное значение при пластовой температуре, то возвращаем его
        pb_atma = p_pb_atma
    Else
    ' иначе считаем что получится из расчета по корреляции по газосодержанию
        pb_atma = calc_pb_atma(rsb_m3m3, tres_C)
    End If
End Property
Property Let pb_atma(val As Double)
    If (val >= 0) Then
        p_pb_atma = val
    ElseIf (val = -1) Then
        p_pb_atma = -1 ' явный сброс значения давления насыщения после которого оно будет вычисляться по корреляции
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "pb_atma"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
End Property
'----- Bo -----------------------------------------------------------------------------------------
Public Property Get bob_m3m3() As Double
    bob_m3m3 = p_bob_m3m3
End Property
Property Let bob_m3m3(val As Double)
    If (val >= 0) Then
        p_bob_m3m3 = val
    ElseIf val = -1 Then
        ' явный сброс значения давления насыщения после которого оно будет вычисляться по корреляции
        p_bob_m3m3 = -1
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "bob_m3m3"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
End Property
Public Property Get muob_cP() As Double
    muob_cP = p_muob_cP
End Property
Property Let muob_cP(val As Double)
    If (val >= 0) Then
        p_muob_cP = val
    ElseIf val = -1 Then
        p_muob_cP = -1 ' явный сброс значения давления насыщения после которого оно будет вычисляться по корреляции
    Else
        ' унифицированная реакция на ошибочный ввод ключевых параметров класса
        Dim msg As String, fname As String
        fname = "muob_cP"
        msg = p_clname & "." & fname & ": input - wrong " & fname & " = " & S(val)
        addLogMsg msg
        Err.Raise kErrPVTinput, p_clname & "." & fname, msg
    End If
End Property
Public Property Get rs_m3m3() As Double
    rs_m3m3 = p_rs_m3m3
End Property
Public Property Get bo_m3m3() As Double
    bo_m3m3 = p_bo_m3m3
End Property
Public Property Get bg_m3m3() As Double
    bg_m3m3 = p_bg_m3m3
End Property
Public Property Get bw_m3m3() As Double
    bw_m3m3 = p_bw_m3m3
End Property
Public Property Get mu_oil_cP() As Double
 mu_oil_cP = p_mu_oil_cP
End Property
Public Property Get mu_wat_cP() As Double
 mu_wat_cP = p_mu_wat_cP
End Property
Public Property Get mu_gas_cP() As Double
 mu_gas_cP = p_mu_gas_cP
End Property
Public Property Get mu_liq_cP() As Double
'
' todo надо уточнить как считать вязкость для смеси - быть может надо холдап использовать
'
    Dim fw_rc_fr As Double
    If q_liq_rc_m3day > 0 Then
        fw_rc_fr = q_wat_rc_m3day / q_liq_rc_m3day
    Else
        fw_rc_fr = p_fw_fr
    End If
    
    mu_liq_cP = (mu_oil_cP * (1 - fw_rc_fr) + _
                mu_wat_cP * fw_rc_fr)
End Property
Public Property Get mu_mix_cP() As Double
'
' todo надо уточнить как считать вязкость для смеси - быть может надо холдап использовать
'
    mu_mix_cP = p_mu_mix_cP
'               (p_mu_oil_cP * p_q_oil_rc_m3day / qliq_m3day + _
'                p_mu_wat_cP * p_q_wat_rc_m3day / qliq_m3day) * (1 - gas_fraction_d) + _
'                p_mu_gas_cP * (1 - gas_fraction_d)
End Property
' кинематическая вязкость смеси в сантистоксах
Public Property Get mu_mix_cSt() As Double
    mu_mix_cSt = p_mu_mix_cP / (p_rho_mix_rc_kgm3 / 1000)
End Property
' массовый расход нефти
Public Property Get mo_kgsec() As Double
    mo_kgsec = q_oil_rc_m3day * rho_oil_rc_kgm3 / const_conver_day_sec
End Property
' массовый расход воды
Public Property Get mw_kgsec() As Double
    mw_kgsec = q_wat_rc_m3day * rho_wat_rc_kgm3 / const_conver_day_sec
End Property
' массовый расход газа
Public Property Get mg_kgsec() As Double
    mg_kgsec = q_gas_rc_m3day * rho_gas_rc_kgm3 / const_conver_day_sec
End Property
Public Property Get Z() As Double
    Z = p_Z
End Property
Public Property Get pcalc_atma() As Double
    pcalc_atma = p_PTcalc.p_atma
End Property
Public Property Get pcalc_MPaa() As Double
    pcalc_MPaa = pcalc_atma * const_convert_atma_MPa
End Property
Public Property Get tcalc_C() As Double
    tcalc_C = p_PTcalc.t_C
End Property
Public Property Get tcalc_K() As Double
    tcalc_K = tcalc_C + const_t_K_min
End Property
Public Property Get tcalc_F() As Double
    tcalc_F = tcalc_C * 1.8 + 32
End Property
Public Property Get mu_deadoil_cP() As Double
    mu_deadoil_cP = p_mu_deadoil_cP
End Property
'===================================================================================
' функции и процедуры
'
'===================================================================================
 
 Private Sub Class_Initialize()
        p_clname = "CPVT"
        
        p_PVT_correlation = Standing_based
        gamma_o = 0.86
        gamma_g = 0.6
        gamma_w = 1
        rp_m3m3 = 100
        rsb_m3m3 = 100
        pb_atma = -1  ' по умолчанию нет калибровок, только корреляция
        bob_m3m3 = -1  ' по умолчанию нет калибровок, только корреляция
        tres_C = 90
        sigma_oil_gas_Nm = const_sigma_oil_Nm
        sigma_wat_gas_Nm = const_sigma_wat_gas_Nm
        fw_perc = 0
        p_qliq_sm3day = 100
        p_q_gas_free_sm3day = 0
        ' для начала для простоты инициализируем теплоемкость флюидов как константы
        ' потом можно будет добавить расчет в зависимости от условий
        p_co_JkgC = 2100
        p_cw_JkgC = 4100
        p_cg_JkgC = 2200
        zCorr = z_BB
        gas_only = False
 End Sub
 
 
 Public Sub Init(Optional ByVal gamma_gas = 0.6, _
                 Optional ByVal gamma_oil = 0.86, _
                 Optional ByVal gamma_wat = 1, _
                 Optional ByVal rsb_m3m3 = 100, _
                 Optional ByVal pb_atma = -1, _
                 Optional ByVal bob_m3m3 = -1, _
                 Optional ByVal PVTcorr = 0, _
                 Optional ByVal tres_C = 90, _
                 Optional ByVal rp_m3m3 = -1, _
                 Optional ByVal muob_cP = -1)
' P давление, атм
' Т температура,  С
' gamma_gas плотность газа
' gamma_oil плотность нефти
        gamma_g = gamma_gas
        gamma_o = gamma_oil
        gamma_w = gamma_wat
        Me.SetRpRsb rp_m3m3, rsb_m3m3
        Me.pb_atma = pb_atma
        Me.tres_C = tres_C
        Me.bob_m3m3 = bob_m3m3
        Me.muob_cP = muob_cP
        PVT_CORRELATION = PVTcorr
 End Sub
 
 Public Function Clone()
        Dim fl As New CPVT
        Call fl.Copy(Me)
        Set Clone = fl
 End Function
 
 Public Function Copy(fl As CPVT)
        PVT_CORRELATION = fl.PVT_CORRELATION
        ZNLF = fl.ZNLF
        gamma_o = fl.gamma_o
        gamma_g = fl.gamma_g
        gamma_w = fl.gamma_w
        SetRpRsb fl.rp_m3m3, fl.rsb_m3m3
        pb_atma = fl.pb_atma
        bob_m3m3 = fl.bob_m3m3
        muob_cP = fl.muob_cP
        tres_C = fl.tres_C
        fw_fr = fl.fw_fr
        qliq_sm3day = fl.qliq_sm3day
        q_gas_free_sm3day = fl.q_gas_free_sm3day
        sigma_oil_gas_Nm = fl.sigma_oil_gas_Nm
        sigma_wat_gas_Nm = fl.sigma_wat_gas_Nm
 End Function
Public Sub calc_PVT_PT(pt As PTtype)
        Call calc_PVT(pt.p_atma, pt.t_C)
End Sub
Public Sub calc_PVT(ByVal p_atma As Double, ByVal t_C As Double)
' расчет свойств воды нефти и газа при заданных давлении и температуре
'    Dim timeStamp
'    timeStamp = Time()
    Dim T_K As Double  ' internal K temp
    'PVT properties
    Dim rho_o As Double
    Dim bob_m3m3_sat As Double
    'internal buffers used to store output values
    Dim p_bi As Double
    Dim r_si As Double
    Dim rho_o_sat As Double
    Dim p_fact As Double
    Dim p_offs As Double
    Dim b_fact As Double
    Dim mu_fact As Double
    'Oil pressure in MPa
    Dim P_MPa As Double
    Dim Pb_calbr_MPa As Double
    Dim rsb_calbr_m3m3 As Double
    Dim Bo_calbr_m3m3 As Double
    Dim muo_calibr_cP As Double
    Dim ranges_good As Boolean
    Dim mu_deadoil_cP As Double
    Dim Muo_saturated_cP As Double
On Error GoTo err1:
    T_K = t_C + const_t_K_min
    p_PTcalc.p_atma = p_atma
    p_PTcalc.t_C = t_C
    rsb_calbr_m3m3 = rsb_m3m3
    Bo_calbr_m3m3 = bob_m3m3
    muo_calibr_cP = muob_cP
    P_MPa = p_atma * const_convert_atma_MPa
    'convert user specified bubblepoint pressure
    Pb_calbr_MPa = p_pb_atma * const_convert_atma_MPa    ' изменил чтобы избежать рекурсии при расчете Pb с нулевым значением
    'for saturated oil calibration is applied by application of factor p_fact to input pressure
    'for undersaturated - by shifting according to p_offs
    'calculate PVT properties
    'calculate water properties at current pressure and temperature
    p_bwSC_m3m3 = gamma_w / 1
    p_salinity_ppm = unf_calc_Sal_BwSC_ppm(p_bwSC_m3m3)
    p_bw_m3m3 = unf_calc_Bw_m3m3(P_MPa, T_K) '* p_BwSC_m3m3
    p_mu_wat_cP = unf_calc_mu_wat_cP(P_MPa, T_K, p_salinity_ppm)   ' откуда тут возьмется соленость?!!!
    'if no calibration gas-oil ratio specified, then set it to some very large value and
    'switch of calibration for bubblepoint and oil formation volume factor
    p_Z = unf_calc_Zgas_d(T_K, P_MPa, p_gamma_g, zCorr)
    p_bg_m3m3 = unf_calc_Bg_z_m3m3(T_K, P_MPa, p_Z)
    p_mu_gas_cP = unf_calc_mu_gas_cP(T_K, P_MPa, p_Z, p_gamma_g)
    If PVT_CORRELATION = Standing_based Then
            mu_deadoil_cP = unf_DeadOilViscosity_Beggs_Robinson(T_K, p_gamma_o) 'dead oil viscosity
            Muo_saturated_cP = unf_SaturatedOilViscosity_Beggs_Robinson(rsb_calbr_m3m3, mu_deadoil_cP) 'saturated oil viscosity Beggs & Robinson
            p_bi = unf_Bubblepoint_Standing(rsb_calbr_m3m3, p_gamma_g, tres_K, p_gamma_o)    ' считаем давление насыщения по корреляции Standing для пластовой температуры при которой заданны калибровочные значения
            ' дальше ищем калибровочные коэффициенты
            'Calculate bubble point correction factor
            If (Pb_calbr_MPa > 0) Then 'user specified
                p_fact = p_bi / Pb_calbr_MPa
            Else ' not specified, use from correlations
                p_fact = 1
            End If
            If (Bo_calbr_m3m3 > 0) Then 'Calculate oil formation volume factor correction factor
                bob_m3m3_sat = unf_FVF_Saturated_Oil_Standing(rsb_calbr_m3m3, p_gamma_g, tres_K, p_gamma_o)  ' значение по корреляции считаем также для пластовой температуры
                b_fact = (Bo_calbr_m3m3 - 1) / (bob_m3m3_sat - 1)
            Else ' not specified, use from correlations
                b_fact = 1
            End If
            If muo_calibr_cP > 0 Then           ' рассчитаем калибровочный коэффициент для вязкости при давлении насыщения
                mu_fact = muo_calibr_cP / Muo_saturated_cP
            Else
                mu_fact = 1
            End If
            p_bi = unf_Bubblepoint_Standing(rsb_calbr_m3m3, p_gamma_g, T_K, p_gamma_o)   ' давление насыщения по корреляции при текущей температуре
            P_MPa = P_MPa * p_fact   ' растянем давление чтобы натянуть его на калиброванное значение
            If P_MPa > p_bi Then 'apply correction to undersaturated oil 'undersaturated oil
                 r_si = rsb_calbr_m3m3   ' результат такое будет
                 bob_m3m3_sat = b_fact * (unf_FVF_Saturated_Oil_Standing(rsb_calbr_m3m3, p_gamma_g, T_K, p_gamma_o) - 1) + 1 ' it is assumed that at pressure 1 atma bo=1
                 p_copmressibility_o_1atm = unf_Compressibility_Oil_VB(rsb_calbr_m3m3, p_gamma_g, T_K, p_gamma_o, P_MPa) 'calculate compressibility at bubble point pressure
                 p_bo_m3m3 = bob_m3m3_sat * Exp(p_copmressibility_o_1atm * (p_bi - P_MPa))
                 p_mu_oil_cP = mu_fact * unf_OilViscosity_Vasquez_Beggs(Muo_saturated_cP, P_MPa, p_bi)  'Vesquez&Beggs
            Else 'apply correction to saturated oil
                r_si = unf_GOR_Standing(P_MPa, p_gamma_g, T_K, p_gamma_o)
                p_bo_m3m3 = b_fact * (unf_FVF_Saturated_Oil_Standing(r_si, p_gamma_g, T_K, gamma_o) - 1) + 1 ''Standing. it is assumed that at pressure 1 atma bo=1
                p_mu_oil_cP = mu_fact * unf_SaturatedOilViscosity_Beggs_Robinson(r_si, mu_deadoil_cP)  'Beggs & Robinson
            End If
    End If
    If PVT_CORRELATION = McCain_based Then
            ranges_good = True
            ranges_good = ranges_good And CheckRanges(T_K, "t_K", const_tMcCain_K_min, const_t_K_max, "температура потока вне диапазона для корреляции маккейна", "calc_PVT (McCain)", True)
            mu_deadoil_cP = unf_DeadOilViscosity_Standing(T_K, p_gamma_o)  'dead oil viscosity
            Muo_saturated_cP = unf_SaturatedOilViscosity_Beggs_Robinson(rsb_calbr_m3m3, mu_deadoil_cP) 'saturated oil viscosity Beggs & Robinson
            p_bi = unf_Bubblepoint_Valko_McCainSI(rsb_calbr_m3m3, p_gamma_g, tres_K, p_gamma_o)
            'Calculate bubble point correction factor
            If (Pb_calbr_MPa > 0) Then 'user specifie
                p_fact = p_bi / Pb_calbr_MPa
            Else ' not specified, use from correlations
                p_fact = 1
            End If
            P_MPa = P_MPa * p_fact
            p_copmressibility_o_1atm = unf_Compressibility_Oil_VB(rsb_calbr_m3m3, p_gamma_g, tres_K, gamma_o, p_bi) 'calculate compressibility at bubble point pressure
            If (Bo_calbr_m3m3 > 0) Then 'Calculate oil formation volume factor correction factor
                  rho_o_sat = unf_Density_McCainSI(p_bi, p_gamma_g, tres_K, gamma_o, rsb_calbr_m3m3, p_bi, p_copmressibility_o_1atm)    ' тут формально есть зависимость от сжимаемости но реально она не влияет (так как расчет идет при давлении насыщения)
                  bob_m3m3_sat = unf_FVF_McCainSI(rsb_calbr_m3m3, p_gamma_g, p_gamma_o * const_rho_ref, rho_o_sat)
                  b_fact = (Bo_calbr_m3m3 - 1) / (bob_m3m3_sat - 1)
            Else ' not specified, use from correlations
                  b_fact = 1
            End If
            p_bi = unf_Bubblepoint_Valko_McCainSI(rsb_calbr_m3m3, p_gamma_g, T_K, p_gamma_o)
            r_si = unf_GOR_VelardeSI(P_MPa, p_bi, p_gamma_g, T_K, p_gamma_o, rsb_calbr_m3m3)
            If P_MPa > p_bi Then 'apply correction to undersaturated oil
                  p_copmressibility_o_1atm = unf_Compressibility_Oil_VB(rsb_calbr_m3m3, p_gamma_g, T_K, p_gamma_o, P_MPa)  'calculate compressibility at bubble point pressure
                  rho_o_sat = unf_Density_McCainSI(p_bi, p_gamma_g, T_K, p_gamma_o, rsb_calbr_m3m3, p_bi, p_copmressibility_o_1atm)
                  bob_m3m3_sat = unf_FVF_McCainSI(rsb_calbr_m3m3, p_gamma_g, p_gamma_o * const_rho_ref, rho_o_sat)
                  bob_m3m3_sat = b_fact * (bob_m3m3_sat - 1) + 1 ' it is assumed that at pressure 1 atma bo=1
                  p_bo_m3m3 = bob_m3m3_sat * Exp(p_copmressibility_o_1atm * (p_bi - P_MPa))
            Else 'apply correction to saturated oil
                  rho_o = unf_Density_McCainSI(P_MPa, p_gamma_g, T_K, p_gamma_o, r_si, p_bi, p_copmressibility_o_1atm)
                  p_bo_m3m3 = b_fact * (unf_FVF_McCainSI(r_si, p_gamma_g, p_gamma_o * const_rho_ref, rho_o) - 1) + 1 ' it is assumed that at pressure 1 atma bo=1
            End If
            If muo_calibr_cP > 0 Then           ' рассчитаем калибровочный коэффициент для вязкости при давлении насыщения
                  If (rsb_calbr_m3m3 < 350) Then
                       mu_fact = muo_calibr_cP / unf_Oil_Viscosity_Standing(rsb_calbr_m3m3, mu_deadoil_cP, p_bi, p_bi)
                  Else
                        mu_fact = muo_calibr_cP / Muo_saturated_cP
                  End If
            Else
                  mu_fact = 1
            End If
            If (rsb_calbr_m3m3 < 350) Then 'Calculate oil viscosity acoording to Standing
                  p_mu_oil_cP = mu_fact * unf_Oil_Viscosity_Standing(r_si, mu_deadoil_cP, P_MPa, p_bi)
            Else 'Calculate according to Begs&Robinson (saturated) and Vasquez&Begs (undersaturated)
               If P_MPa > p_bi Then 'undersaturated oil
                  p_mu_oil_cP = mu_fact * unf_OilViscosity_Vasquez_Beggs(Muo_saturated_cP, P_MPa, p_bi)
               Else 'saturated oil
                 'Beggs & Robinson
                 p_mu_oil_cP = mu_fact * unf_SaturatedOilViscosity_Beggs_Robinson(r_si, mu_deadoil_cP)
               End If
            End If
    End If
    If PVT_CORRELATION = 2 Then  'Debug mode. Linear Rs and bo vs P, Pb_calbr_atma should be specified.
         'gas properties
         p_Z = 0.95 'ideal gas
         p_bg_m3m3 = unf_calc_Bg_z_m3m3(T_K, P_MPa, p_Z)
         p_mu_gas_cP = 0.0000000001
         p_fact = 1         'Set to default. b_rb should be specified by user!
         p_offs = 0
         If Pb_calbr_MPa <= 0 Then
            ' can not be estimated without calibration properties
            Err.Raise kErrPVTinput, "CPVT.calc_PVT", "PVT correlation = 2 without Pb input not allowed"
         End If
         p_bi = Pb_calbr_MPa
         If P_MPa > (p_bi) Then 'undersaturated oil
            r_si = rsb_calbr_m3m3
         Else 'saturate
            r_si = P_MPa / Pb_calbr_MPa * rsb_calbr_m3m3
         End If
         'if bob_m3m3 is not specified by the user then
         'set bob_m3m3 so, that oil density, recalculated with Rs_m3m3 would be equal to dead oil density
         If (Bo_calbr_m3m3 <= 0) Then
            addLogMsg ("warning:CPVT.calc_PVT, PVT correlation = 2 without Bob input")
            p_bo_m3m3 = (1 + r_si * (p_gamma_g * const_rho_air) / (p_gamma_o * const_rho_ref))
         Else
            If P_MPa > (p_bi) Then 'undersaturated oil
                p_bo_m3m3 = Bo_calbr_m3m3
            Else 'saturate
                p_bo_m3m3 = 1 + (Bo_calbr_m3m3 - 1) * ((P_MPa - const_convert_atma_MPa) / (p_bi - const_convert_atma_MPa))
            End If
         End If
         If muo_calibr_cP >= 0 Then
             p_mu_oil_cP = muo_calibr_cP
         Else
            addLogMsg ("warning:CPVT.calc_PVT, PVT correlation = 2 without Muob input")
            p_mu_oil_cP = 1
         End If
    End If
    'Assign output variables
    p_pbcalc_atma = p_bi / p_fact / const_convert_atma_MPa
    p_rs_m3m3 = r_si
    p_mu_deadoil_cP = mu_deadoil_cP
    p_q_oil_rc_m3day = p_qliq_sm3day * (1 - p_fw_fr) * p_bo_m3m3   ' для ускорения расчетов потом все что можно подсчитаем тут
    p_q_wat_rc_m3day = p_qliq_sm3day * p_fw_fr * p_bw_m3m3
    p_q_gas_rc_m3day = (p_qliq_sm3day * (1 - p_fw_fr) * p_rp_m3m3 + p_q_gas_free_sm3day - p_rs_m3m3 * p_qliq_sm3day * (1 - p_fw_fr)) * p_bg_m3m3
    p_q_liq_rc_m3day = p_q_wat_rc_m3day + p_q_oil_rc_m3day
    If (p_q_wat_rc_m3day + p_q_oil_rc_m3day + p_q_gas_rc_m3day) > 0 Then
        p_gas_fraction_d = p_q_gas_rc_m3day / (p_q_wat_rc_m3day + p_q_oil_rc_m3day + p_q_gas_rc_m3day)
        p_mu_mix_cP = (p_mu_oil_cP * p_q_oil_rc_m3day / p_q_liq_rc_m3day + _
                  p_mu_wat_cP * p_q_wat_rc_m3day / p_q_liq_rc_m3day) * (1 - p_gas_fraction_d) + _
                  p_mu_gas_cP * (1 - p_gas_fraction_d)
    Else
        p_gas_fraction_d = 0
        p_mu_mix_cP = 0
    End If
    p_rho_oil_rc_kgm3 = 1000 * (p_gamma_o + p_rs_m3m3 * p_gamma_g * const_rho_air / 1000) / p_bo_m3m3
    p_rho_wat_rc_kgm3 = 1000 * (p_gamma_w) / p_bw_m3m3
    p_rho_liq_rc_kgm3 = (1 - p_fw_fr) * rho_oil_rc_kgm3 + p_fw_fr * rho_wat_rc_kgm3
    p_rho_mix_rc_kgm3 = rho_liq_rc_kgm3 * (1 - f_g) + rho_gas_rc_kgm3 * f_g
    
    Call calc_ST(p_atma, t_C)
'    timeStamp = Time() - timeStamp
'    timePVTtotal = timePVTtotal + timeStamp
    Exit Sub
err1:
    Dim errmsg As String
    errmsg = "Error:CPVT.calc_PVT:" & Err.Description
    addLogMsg (errmsg)
    Err.Raise kErrPVTcalc, "CPVT.calc_PVT", errmsg
End Sub
Private Sub calc_ST(ByVal p_atma As Double, ByVal t_C As Double)
' calculate surface tension according Baker Sverdloff correlation
On Error GoTo err1:
'Расчет коэффициента поверхностного натяжения газ-нефть
    Dim ST68 As Double, ST100 As Double
    Dim STw74 As Double, STw280 As Double
    Dim Tst As Double, Tstw As Double
    Dim STo As Double, STw As Double, ST As Double
    Dim t_F As Double
    Dim P_psia As Double, P_MPa As Double
        t_F = t_C * 1.8 + 32
        P_psia = p_atma / 0.068046
        P_MPa = p_atma / 10
        ST68 = 39 - 0.2571 * oil_API
        ST100 = 37.5 - 0.2571 * oil_API
        If t_F < 68 Then
            STo = ST68
        Else
            Tst = t_F
            If t_F > 100 Then Tst = 100
            STo = (68 - (((Tst - 68) * (ST68 - ST100)) / 32)) * (1 - (0.024 * (P_psia) ^ 0.45))
            If STo < 0 Then STo = ST68  ' todo rnt20190312 надо будет исправить когда то
        End If
        'Расчет коэффициента поверхностного натяжения газ-вода  (два способа)
        STw74 = (75 - (1.108 * (P_psia) ^ 0.349))
        STw280 = (53 - (0.1048 * (P_psia) ^ 0.637))
        If t_F < 74 Then
            STw = STw74
        Else
            Tstw = t_F
            If t_F > 280 Then Tstw = 280
            STw = STw74 - (((Tstw - 74) * (STw74 - STw280)) / 206)
        End If
        ' далее второй способ
        STw = 10 ^ (-(1.19 + 0.01 * P_MPa)) * 1000
        ' Расчет коэффициента поверхностного натяжения газ-жидкость
        ST = (STw * p_fw_fr) + STo * (1 - p_fw_fr)
        p_ST_oilgas_dyncm = STo
        p_ST_watgas_dyncm = STw
        p_ST_liqgas_dyncm = ST
        
    Exit Sub
err1:
    Dim errmsg As String
    errmsg = "Error:CPVT.calc_ST:" & Err.Description
    addLogMsg (errmsg)
    Err.Raise kErrPVTcalc, "CPVT.calc_ST", errmsg
End Sub
Public Function calc_rs_m3m3(ByVal p_atma As Double, ByVal t_C As Double) As Double
'function calculates solution gas oil ratio
    Call calc_PVT(p_atma, t_C)
    calc_rs_m3m3 = p_rs_m3m3
End Function
Public Function calc_pb_atma(ByVal rsb_m3m3 As Double, ByVal t_C As Double) As Double
'function calculates oil bubble point pressure
    p_rsb_m3m3 = rsb_m3m3
    Call calc_PVT(1, t_C)
    calc_pb_atma = p_pbcalc_atma
End Function
Public Function calc_bo_m3m3(ByVal p_atma As Double, ByVal t_C As Double) As Double
'Function calculates oil formation volume factor
    Call calc_PVT(p_atma, t_C)
    calc_bo_m3m3 = p_bo_m3m3
End Function
Public Function calc_mu_oil_cP(ByVal p_atma As Double, ByVal t_C As Double) As Double
'function calculates oil viscosity
    Call calc_PVT(p_atma, t_C)
    calc_mu_oil_cP = p_mu_oil_cP
End Function
'Public Function Qgas_cas_scm3day(Optional ByVal Ksep As Double = -1) As Double
'   If Ksep > 0 And Ksep < 1 Then
'        Qgas_cas_scm3day = q_gas_rc_m3day * Ksep
'   Else
'        Qgas_cas_scm3day = 0
'   End If
'End Function
Public Function gas_fraction_d(Optional ByVal Ksep As Double = 0) As Double
' метод расчета доли газа в потоке для заданной жидкости при заданных условиях
' предполагается что свойства нефти газа и воды уже расчитаны и заданы при необходимых условиях
On Error GoTo err1:
    Dim qmix As Double
    gas_fraction_d = 0
    qmix = q_mix_rc_m3day   ' сохраним чтобы немного сэкономить на проверку нулевого значения
    If qmix > 0 And Ksep >= 0 And Ksep < 1 Then
        gas_fraction_d = q_gas_rc_m3day * (1 - Ksep) / (q_wat_rc_m3day + q_oil_rc_m3day + q_gas_rc_m3day * (1 - Ksep))
'    Else
'        addLogMsg "gas_fraction_d: Общий расход ГЖС (qo + qw + qg) = " & qmix
    End If
    Exit Function
err1:
    Dim errmsg As String
    errmsg = "Error:CPVT.gas_fraction_d:" & Err.Description
    addLogMsg errmsg
    gas_fraction_d = 0
   ' Err.Raise kErrPVTcalc, "CPVT.gas_fraction_d", errmsg
End Function
Public Function p_gas_fraction_atma(FreeGas, t_C As Double, Optional Es As Double = 0, Optional P_init_atma = 300) As Double
    'P_init     - давление инициализации, атм
    'FreeGas    - доля газ на приеме целевая
    'Es         - коэффициент сепарации насоса
    Dim p1 As Double
    Dim p2 As Double
    Dim max_iter As Integer, i As Integer
    Dim E As Double
    Dim p_gas As Double, P As Double
    max_iter = 100
    E = 0.0001
    p1 = P_init_atma
    p2 = 0
On Error GoTo err1:
    For i = 1 To max_iter
        P = (p1 + p2) / 2
        Call calc_PVT(P, t_C)
        p_gas = gas_fraction_d(Es)
        If Abs(p_gas - FreeGas) <= E Then Exit For
        If p_gas > FreeGas Then
           p2 = P
        Else
           p1 = P
        End If
    Next
    p_gas_fraction_atma = P
    Exit Function
err1:
    p_gas_fraction_atma = 0
End Function
Public Function rp_gas_fraction_m3m3(FreeGas, p_atma As Double, t_C As Double, Optional Es As Double = 0, Optional Rp_init_m3m3 = 500) As Double
    'P_init     - давление инициализации, атм
    'FreeGas    - доля газ на приеме целевая
    'Es         - коэффициент сепарации насоса
    Dim G1 As Double
    Dim g2 As Double
    Dim max_iter As Integer, i As Integer
    Dim E As Double
    Dim p_gas As Double, G As Double
    max_iter = 100
    E = 0.0001
    G1 = Rp_init_m3m3
    g2 = 0
On Error GoTo err1:
    For i = 1 To max_iter
        G = (G1 + g2) / 2
        rp_m3m3 = G
        Call calc_PVT(p_atma, t_C)
        p_gas = gas_fraction_d(Es)
        If Abs(p_gas - FreeGas) <= E Then Exit For
        If p_gas > FreeGas Then
           G1 = G
        Else
           g2 = G
        End If
    Next
    rp_gas_fraction_m3m3 = G
    Exit Function
err1:
    rp_gas_fraction_m3m3 = 0
End Function
Public Function get_clone_mod_after_separation(p_atma As Double, t_C As Double, Ksep As Double, _
                                         Optional ByVal GasSol As GAS_INTO_SOLUTION = GasnotGoesIntoSolution) As CPVT
    Dim newfluid As CPVT
    Set newfluid = Me.Clone
    Call newfluid.mod_after_separation(p_atma, t_C, Ksep, GasSol)
    Set get_clone_mod_after_separation = newfluid
End Function
Public Sub mod_after_separation(ByVal p_atma As Double, ByVal t_C As Double, ByVal Ksep As Double, _
                                         Optional ByVal GasSol As GAS_INTO_SOLUTION = GasnotGoesIntoSolution) 'As CPVT
' функция модификации свойств нефти после сепарации
' удаление части газа меняет свойства нефти - причем добавление газа свойства не трогает
' на входе условия при которых проходила сепарация
    Dim Rs As Double
    Dim Bo As Double
    Dim Pb_Rs_curve As New CInterpolation ' хранилище кривой зависимости газосодержания от давления насыщения
    Dim Bo_Rs_curve As New CInterpolation
    Dim pb_atma_tab As Double, rsb_m3m3_tab As Double, Bo_m3m3_tab As Double
    Dim Delta As Double
    Dim i As Integer
    Const N = 10
    Dim Rpnew_with_Ksep As Double   ' новый ГФ с учетом сепарации газа
    Dim Rpnew_Ksep_1 As Double      ' новый ГФ без учета сепарации
    Dim Rpnew As Double
        ' найдем сколько газа осталось в растворе при условиях сепарации
        
On Error GoTo err1:
        
    With Me
        Rs = .calc_rs_m3m3(p_atma, t_C)
        Bo = .calc_bo_m3m3(p_atma, t_C)
        
        ' оценим как изменится газовый фактор за счет ухода части газа из потока
        ' оцениваем двумя способами - буду влиять на работу опции растворения газа (фазовой неравновесности)
        '   - с учетом сепарации (показывает новый ГФ если газ может потом растворяться)
        '   - без учета сепарации (Ksep = 1 показывает ГФ если газ может только сжиматься)
        
        Rpnew_with_Ksep = .rp_m3m3 - (.rp_m3m3 - Rs) * Ksep
        Rpnew_Ksep_1 = .rp_m3m3 - (.rp_m3m3 - Rs)
        
        
        Delta = (.pb_atma - 1) / N    ' считать будет только в диапазоне где определены Pb за ним будет линейно экстраполировать
        ' запишем зависимость газосодержания от давления насыщения на память
        For i = 0 To N
            pb_atma_tab = 1 + Delta * i
            rsb_m3m3_tab = .calc_rs_m3m3(pb_atma_tab, .tres_C)
            Pb_Rs_curve.AddPoint rsb_m3m3_tab, pb_atma_tab
            Bo_m3m3_tab = .calc_bo_m3m3(pb_atma_tab, .tres_C)
            Bo_Rs_curve.AddPoint rsb_m3m3_tab, Bo_m3m3_tab
        Next i
        ' найдем сколько всего газа осталось в потоке
        
        If GasSol = GasGoesIntoSolution Then   ' тогда газ успеет растворится
            Rpnew = Rpnew_with_Ksep
        Else                                   ' газ не растворяется, то же самое, что Ксеп = 1
            Rpnew = Rpnew_Ksep_1
        End If
        
        If Rpnew < .rsb_m3m3 Then
            ' Если газовый фактор становится меньше газосодержания, тогда надо скорректировать газосодержание и давление насыщения,
            ' которое будет от него зависеть
            .pb_atma = Pb_Rs_curve.getPoint(Rpnew)
            .bob_m3m3 = Bo_Rs_curve.getPoint(Rpnew)
            .rsb_m3m3 = Rpnew
            ' иначе газа из раствора не сепарировался - свойства не менялись ничего делать не надо
        End If
        ' итоговый газовый фактор всегда с учетом сепараци
        .rp_m3m3 = Rpnew_with_Ksep
    End With
     Exit Sub
err1:
    Dim errmsg As String
    errmsg = "Error:CPVT.mod_after_separation:" & Err.Description
    addLogMsg errmsg
    Err.Raise kErrPVTcalc, "CPVT.mod_after_separation", errmsg
End Sub
