'=======================================================================================
'Unifloc 7.9  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
'
' класс описывает систему ЭЦН в простом варианте - один ЭЦН+ПЭД+кабель
' класс отвечатает и за расчет сепарации и свойств флюидов
Option Explicit
Private p_ESPpump As CESPpump  ' сам насос
' описание газосепаратора
Public isGasSep As Boolean
Public KSepGasSep_fr As Double           ' коэффициент сепарации газосепаратора
Public dPower_GasSep_W As Double
' параметры сепарации газа на приеме насоса
' при расчете снизу вверх могут быть рассчитаны по корреляциям
' могут быть использованы значения заданные вручную
' при расчете сверху вниз значения параметров сепарации должны быть заданы вручную
' должны быть заданы и значения давления и температуры сепарации
Public isManualSeparationCalc As SEPAR_CALC_METHOD   ' флаг для ручного задания параметров сепарации
                                           ' 0 - нет - расчет автоматический
                                           ' 1 - Ксеп ручной, давления автомат
                                           ' 2 - Ксеп ручной, давления и температура ручные
Public KsepNat_fr As Double
' property KsepTotal_fr As Double  defined below
' для расчета сепарации надо либо задать, либо спасти значения при которых проводился расчет сепарации
Public Psep_atma As Double
Public Tsep_C As Double
Public tintake_C As Double
Public t_dis_C As Double
' простой набор параметров для описания УЭЦН
Public U_V As Double                ' напряжение на ПЭД фактическое
Private p_U_trans_high_V As Double    ' напряжение на трансформатере на высокой стороне, расчетное
Private p_U_trans_low_V As Double     ' напряжение на трансформатере на низкой стороне, константа по факту
                                    ' todo логично было бы задавать напряжение на поверхности, а считать какое
                                    ' будет внизу - но это усложняе расчет и отложено на потом
Public power_motor_nom_W As Double  ' номинальная мощность ПЭД на номинальной частоте
Private p_power_motor_W As Double       ' расчетная мощность на ПЭД, только читать
Private p_power_motor_fact_W As Double ' фактическая мощность ПЭД, задается для коррекции мощности
Private p_Energy_fact_Whday As Double  ' фактическое потребление энергии за сутки Втч/день
Public freq_nom_Hz As Double        ' номинальная частота вращения ЭЦН из базы ЭЦН
Public motor_eff_fr As Double       ' номинальный КПД ПЭД todo заменить на характеристик ПЭД
Public CS_eff_fr As Double          ' номинальный КПД станции управления УЭЦН
Public Transform_eff_fr As Double   ' номинальный КПД трансформатора УЭЦН
Public dPower_CS_W As Double        ' потребление мощности станцией управления
Public dPower_transform_W As Double ' потребление мощности трансформатором
Public dPower_protector_W As Double ' потребление мощности гидрозашитой
Public power_shaft_W As Double      ' мощность механическая на валу
Public power_CS_calc_W As Double        ' мощность на входе в станцию управления (с учетом корректировки) факт
Public power_CS_teor_calc_W As Double   ' мощность на входе в станцию управления  расчетная без корретировки
' параметры описывающие кабельную систему
Public cable_type_num As Integer    ' номер кабеля todo сделать базу кабелей
Public cable_name As String         ' название кабеля
Public cable_R_Omkm As Double       ' удельное сопротивление
Public cable_Tmax_C As Double       ' температурный рейтинг  todo сделать проверки на температурный рейтинг
Public cable_dU_V As Double         ' падение напряжения на кабеле
Public cable_dPower_W As Double     ' мощность потребляемая кабелем
Public cable_power_W As Double      ' мощность на входе в кабель
Public p_freq_Hz As Double          ' частота питающего напряжения, Гц
Public slip_fr As Double            ' номинальная величина проскальзывания
                                    ' в отличии от насоса где частота соответствует частоте вращения вала
                                    ' в системе УЭЦН это частота тока и во вращение транслируется с проскальзыванием
Public cos_phi As Double            ' коэффициент мощности
Public I_A As Double                ' ток рабочий
Public load_fr As Double            ' загрузка ПЭД УЭЦН
                                    ' отношение фактической потребляемой мощности при работе
                                    ' к номинальной скорректированной на частоту
' параметры установки УЭЦН в скважине
Private p_HmesTop_m As Double
Private p_ESP_length_m As Double
Public d_cas_mm As Double
Public d_intake_mm As Double
Public Angle_deg As Double
' флюиды которые работают в системе УЭЦН
Private p_fluid As CPVT    ' флюид перед входом в насос
Public fluidTub As CPVT    ' флюид в НКТ выше насоса
Public fluidCas As CPVT    ' флюид в затрубном пространстве
Public fluidGasSep As CPVT ' флюид в газосепараторе
'
Private p_PKV_work_min As Double     ' время работы в ПКВ режиме
Private p_PKV_stop_min As Double     ' время ожидания в ПКВ режиме
' параметры определяющие режим периодической кратковременной работы скважины
' ПКВ режим влияет на расчет показателей УЭЦН
'   - учитывается, что в период работы дебит равен мгновенному
'   - в период остановки дебита в системе нет
'
' класс ESPSystemSimple работает по следующей схеме
'   -  задает ЭЦН
'   -  задает флюид который притекает в зону приема УЭЦН
'   -  определяет сепарацию и распределяет флюиды по элементам системы
'         - что в насос
'         - что в НКТ (может отличаться от насоса для ПКВ режима)
'         - что в затруб
'   - задает электрические показатели системы УЭЦН и проводит расчет системы
'   - тепературный режим регулируется параметрами класса
'           Tintake_C - температура на приеме насоса
'           t_dis_C - температура на выкиде насоса. если =-1 то рассчитывается при calc_along_flow = True
Public Property Get freq_Hz() As Double
   freq_Hz = p_freq_Hz
End Property
Public Property Let freq_Hz(val As Double)
   p_freq_Hz = val
   p_ESPpump.freq_Hz = val * (1 - slip_fr)
End Property
Public Property Get ESPpump() As CESPpump
   Set ESPpump = p_ESPpump
End Property
Public Property Set ESPpump(pmp As CESPpump)
   Set p_ESPpump = pmp
   freq_nom_Hz = pmp.nom_freq_Hz
End Property
Public Property Get power_motor_fact_W() As Double
   power_motor_fact_W = p_power_motor_fact_W
End Property
Public Property Get Energy_fact_Whday() As Double
   Energy_fact_Whday = p_Energy_fact_Whday
End Property
Public Property Let Energy_fact_Whday(val As Double)
   p_Energy_fact_Whday = val
   p_power_motor_fact_W = val / 24 / PKV_frac
End Property
Public Property Get power_motor_W() As Double
   power_motor_W = p_power_motor_W
End Property
Public Property Get U_trans_high_V() As Double
' напряжение на трансфоратере по высокой стороне
    U_trans_high_V = p_U_trans_high_V
End Property
Public Property Get U_trans_low_V() As Double
' напряжение на трансфоратере по низкой стороне
    U_trans_low_V = p_U_trans_low_V
End Property
Public Property Get trans_coef_fr() As Double
' коэффициент трансформации трансформатора
    trans_coef_fr = U_trans_high_V / U_trans_low_V
End Property
Public Property Get isPKV() As Boolean
' оценка работает ли скважина в ПКВ режиме
    isPKV = p_PKV_work_min > 0 And p_PKV_stop_min > 0
End Property
Public Property Let isPKV(val As Boolean)
' нужна для отключения ПКВ режима
    If val Then
        Call setPKV(30, 30)
    Else
        Call setPKV(-1, -1)
    End If
End Property
Public Sub setPKV(ByVal PKV_work_min As Double, _
                  ByVal PKV_stop_min As Double)
' устновка ПКВ режима
' в виде отдельной процедуры, так как надо при этом модифицировать флюид
    p_PKV_work_min = PKV_work_min
    p_PKV_stop_min = PKV_stop_min
    If isPKV Then
        Set p_ESPpump.fluid = fluid
    End If
End Sub
Private Sub PKV_update()
    ' обновляем все параметры чтобы лучше работать с ПКВ
    
    ' корректируем дебит
    ESP.fluid.qliq_sm3day = ESP.fluid.qliq_sm3day / PKV_frac
    p_power_motor_fact_W = Energy_fact_Whday / 24 / PKV_frac
End Sub
' свойства для управления флюидом
Public Property Get fluid() As CPVT
    Set fluid = p_fluid
End Property
Public Property Set fluid(val As CPVT)
    Set p_fluid = val
    ' сбросим также все другие флюиды
    Set fluidTub = val
    Set fluidCas = val
    Set fluidGasSep = val
    ' в насосе всегда свой флюид
    Set p_ESPpump.fluid = val.Clone()
    Call PKV_update
End Property
Public Property Get KsepTotal_fr() As Double
' общая сепарация УЭЦН параметр только для чтения
' для задания вручную надо задать естественную сепарацию и сепарацию газосепаратора
    KsepTotal_fr = KsepNat_fr + (1 - KsepNat_fr) * KSepGasSep_fr
End Property
Public Property Get HmesTop_m() As Double
    HmesTop_m = p_HmesTop_m
End Property
Public Property Let HmesTop_m(val As Double)
    p_HmesTop_m = val
End Property
Public Property Get HmesBottom_m() As Double
    HmesBottom_m = p_HmesTop_m + p_ESP_length_m
End Property
Private Sub Class_Initialize()
    ' парметры установки по умолчанию
    d_cas_mm = 150
    d_intake_mm = 100
    Angle_deg = 90
    HmesTop_m = 1000
    p_ESP_length_m = const_ESP_length
    
    U_V = 1000
    p_U_trans_low_V = 380
    
    power_motor_nom_W = 30000
    freq_nom_Hz = 50
    motor_eff_fr = 0.9
    CS_eff_fr = 0.97
    Transform_eff_fr = 0.97
    Call setCable(1)
    cable_dU_V = -1
    p_freq_Hz = 50
    slip_fr = 0.05
    cos_phi = 0.9
    
    dPower_protector_W = 400
    dPower_GasSep_W = 500
    
    isGasSep = True
    isManualSeparationCalc = True
    KsepNat_fr = 0.5
    KSepGasSep_fr = 0.5
    Psep_atma = 100
    Tsep_C = 60
End Sub
Public Sub setCable(ByVal cable_type As Integer)
    Select Case cable_type
        Case 1
            ' http://pskovgeokabel.ru/products/1_16/
            cable_R_Omkm = 1.18
            cable_name = "КПпАпБП-120 3x16"
            cable_Tmax_C = 120
    End Select
    ' здесь добавить другие марки кабеля при необходимости
    ' потом когда нибудь можно будет и расчет параметров кабеля сделать в термовставку тоже
    cable_type_num = cable_type
End Sub
Public Function calc_separation(p_atma As Double, t_C As Double)
' расчет сепарации и соответствующих параметров флюидов
' должен вызываться незавимо от расчета УЭЦН, так как при расчете сверху вниз
'   необходимо заранее рассчитать параметры флюида выше насоса, чтобы правильно посчитать НКТ
' p_atma  - давление при которой идет сепарация
' T_C - температура при которой идет сепарация
    Select Case isManualSeparationCalc
        Case byCorrealation
            ' все по сепарации по корреляциям
            Psep_atma = p_atma
            Tsep_C = t_C
            Call p_fluid.calc_PVT(Psep_atma, Tsep_C)
            With p_fluid
                KsepNat_fr = unf_calc_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, _
                                            .qliq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, _
                                            .sigma_oil_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
                                    
            End With
        Case pressureManual
            ' берем значение ручное, давления как есть
             Call p_fluid.calc_PVT(Psep_atma, Tsep_C)
             With p_fluid
                KsepNat_fr = unf_calc_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, _
                                            .qliq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, _
                                            .sigma_oil_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
                                    
             End With
        Case valueManual
            ' берем и коэффициенты и давления как было задано при установке значений
            Psep_atma = p_atma
            Tsep_C = t_C
            Call p_fluid.calc_PVT(Psep_atma, Tsep_C)
        Case fullyManual
            ' берем и коэффициенты и давления как было задано при установке значений
            Call p_fluid.calc_PVT(Psep_atma, Tsep_C)
        
    End Select
    
    ' потом, когда будет расчет газосепаратора - то надо будет разделить алгоритмы
    ' ручного задания и задания с использованием газосепаратора
    If isGasSep Then
        KSepGasSep_fr = KSepGasSep_fr
    Else
        KSepGasSep_fr = 0
    End If
    
    Set fluidTub = p_fluid.Clone()
    Call fluidTub.mod_after_separation(Psep_atma, Tsep_C, KsepTotal_fr, GasGoesIntoSolution)
    Set fluidCas = p_fluid.Clone() ' тут надо будет доделать чтобы считать затруб с газом
    fluidCas.q_gas_free_sm3day = fluid.q_gas_insitu_sm3day * KsepTotal_fr
    Set fluidGasSep = p_fluid.Clone()
    Call fluidGasSep.mod_after_separation(Psep_atma, Tsep_C, KSepGasSep_fr, GasGoesIntoSolution)
    ' в насосе свой флюид отличный от НКТ
    Set p_ESPpump.fluid = fluidTub.Clone()
    Call PKV_update
End Function
Public Property Get PKV_frac() As Double
    If isPKV Then
        PKV_frac = p_PKV_work_min / (p_PKV_work_min + p_PKV_stop_min)
    Else
        PKV_frac = 1
    End If
End Property
Public Function calc_ESPsys(p_atma As Double, _
                            Optional ByVal calc_along_flow As Boolean = True, _
                            Optional ByVal calc_cfPower = False, _
                            Optional ByVal calc_temperature = False) As PTtype
' функция расчета параметров работы УЭЦН
'
' p_atma - давление начала расчета (давление на приеме если calc_along_flow = True)
' calc_along_flow - флаг показывает ведется ли расчет от давления на приеме или от выкида
'                  при расчете сверху вниз температура должна быть задана с двух сторон
'                  и сепарация будет учитываться в соответствии с параметрами заданными вручную
' calc_cfPower - флаг режима расчета корректировочного фактора для потребления энергии
' calc_temperature - флаг режима расчета перепада температуры в ЭЦН
    
    ' подготовим режима расчета температуры для ЭЦН
    If calc_temperature Then
        t_dis_C = -1
    ElseIf t_dis_C < 0 Then
        t_dis_C = tintake_C
    End If
  
    '1. расчет распределения давления в ЭЦН при работе УЭЦН
    Call p_ESPpump.calc_ESP(p_atma, tintake_C, t_dis_C, calc_along_flow)
    '2. Оценим мощность на валу
    power_shaft_W = p_ESPpump.PowerESP_Wt + dPower_protector_W + dPower_GasSep_W
    '3. Оценим электрическую мощность потребляемую ПЭД
    p_power_motor_W = power_shaft_W / motor_eff_fr
    '4. оценим потери в кабеле - для этого найдем примерно ток двигателя
    I_A = p_power_motor_W / 1.732050808 / U_V / cos_phi
    '5 по данным тока и сопротивления кабеля найдем падение напряжения в кабеле
    cable_dU_V = I_A * cable_R_Omkm * HmesTop_m / 1000
    p_U_trans_high_V = U_V + cable_dU_V
    '6 по падению напряжения оценим мощность рассеиваемую кабелем в тепло. учитываем 3 жилы. Тут учитывается полный ток и активный и реактивный
    cable_dPower_W = 3 * cable_dU_V * I_A
    '7 найдет мощность электрическую на входе в кабель
    cable_power_W = p_power_motor_W + cable_dPower_W
    '8. оценим потери мощность в трансформаторе и станции управления
    dPower_transform_W = (1 - CS_eff_fr) * cable_power_W
    dPower_CS_W = (1 - Transform_eff_fr) * (cable_power_W + dPower_transform_W)
    '9. оценим общую потребляемую мощность УЭЦН
    power_CS_teor_calc_W = cable_power_W + dPower_transform_W + dPower_CS_W
    If calc_cfPower Then
        p_ESPpump.c_calibr_power = 1 - p_power_motor_fact_W / power_CS_teor_calc_W
    End If
    '10. внесем в мощность поправку на деградацию по мощности
    power_CS_calc_W = power_CS_teor_calc_W * (1 - c_calibr_power)
    ' 11 рассчитаем загрузку двигателя, для этого получим номинальную мощность на данной частоте
    Dim PowNom_corr_W As Double
    PowNom_corr_W = power_motor_nom_W * freq_Hz / freq_nom_Hz   ' потом надо проверить что именно так считается номинальная мощность
    load_fr = p_power_motor_W / PowNom_corr_W
    
    
    calc_ESPsys = SetPT(p_ESPpump.Pdis_atma, p_ESPpump.t_dis_C)
End Function
 Public Property Get PTsep() As PTtype
    PTsep = SetPT(Psep_atma, Tsep_C)
 End Property
 Public Sub setKseptotal(calc_method As SEPAR_CALC_METHOD, KsepNat_fr As Double, _
                         Optional KsepGS_fr As Double = 0, _
                         Optional Psep_atma As Double = -1, _
                         Optional Tsep_C As Double = -1)
 ' устанавливает значение сепарации для уэцн вручную
 ' сначала установим значение газосепаратора - оно всегда одинаково для всех вариантов
    
    If KsepGS_fr > 0 And KsepGS_fr <= 1 Then
        isGasSep = True
    Else
       isGasSep = False
    End If
    KSepGasSep_fr = KsepGS_fr
    
    Me.Psep_atma = Psep_atma
    Me.Tsep_C = Tsep_C
    Me.KsepNat_fr = KsepNat_fr
    
    ' установим метод расчета
    ' но так чтобы он не противоречил введенным данным
     If KsepNat_fr >= 0 And KsepNat_fr <= 1 And PTsep.p_atma > 0 And PTsep.t_C > 0 Then
        ' если ввели хорошие параметры то можно применить любой метод расчета
         isManualSeparationCalc = calc_method
     ElseIf KsepNat_fr >= 0 And KsepNat_fr <= 1 And PTsep.p_atma <= 0 Then
        ' плохое значение давления не позволяет полностью ручной метод
        If calc_method <> fullyManual Then
            isManualSeparationCalc = calc_method
        Else
            isManualSeparationCalc = valueManual
        End If
     ElseIf (KsepNat_fr < 0 Or KsepNat_fr > 1) And (PTsep.p_atma > 0 And PTsep.t_C > 0) Then
        ' плохая сепарация, но хорошие давления
         If calc_method = byCorrealation Or calc_method = pressureManual Then
            isManualSeparationCalc = calc_method
         Else
            isManualSeparationCalc = pressureManual
        End If
     Else
         ' плохая сепарация не позволяет ручной расчет
         isManualSeparationCalc = byCorrealation
     End If
     
 End Sub
 ' дебит газожидкостной смеси перед насосом в рабочих условиях
 Public Property Get QmixBeforePump_m3day() As Double
   fluid.calc_PVT ESP.Pint_atma, ESP.tintake_C
   QmixBeforePump_m3day = fluid.q_mix_rc_m3day
 End Property
 
 ' дебит газожидкостной смеси внути газосепаратора (после естественной сепарации)
 Public Property Get QmixInGasSep_m3day() As Double
   ' надо рассчитать свойства флюида после частичной сепарации
   fluidGasSep.calc_PVT ESP.Pint_atma, ESP.tintake_C
   QmixInGasSep_m3day = fluidGasSep.q_mix_rc_m3day
 End Property
 ' дебит газожидкостной смеси в насосе на первых ступенях (после естественной сепарации и газосепаратора)
 Public Property Get QmixPumpIntake_m3day() As Double
   fluidTub.calc_PVT ESP.Pint_atma, ESP.tintake_C
   QmixPumpIntake_m3day = fluidTub.q_mix_rc_m3day
 End Property
 
 ' дебит газожидкостной смеси на выкиде насоса (на последних ступенях)
 Public Property Get QmixPumpOutlet_m3day() As Double
   fluidTub.calc_PVT ESP.Pdis_atma, ESP.t_dis_C
   QmixPumpOutlet_m3day = fluidTub.q_mix_rc_m3day
 End Property
 
 Public Property Get GasFraction_BeforePump_d() As Double
    fluid.calc_PVT ESP.Pint_atma, ESP.tintake_C
    GasFraction_BeforePump_d = fluid.gas_fraction_d(0)
 End Property
 
 Public Property Get GasFraction_InGasSep_d() As Double
    fluidGasSep.calc_PVT ESP.Pint_atma, ESP.tintake_C
    GasFraction_InGasSep_d = fluidGasSep.gas_fraction_d(0)
 End Property
 Public Property Get GasFraction_PumpIntake_d() As Double
    fluidTub.calc_PVT ESP.Pint_atma, ESP.tintake_C
    GasFraction_PumpIntake_d = fluidTub.gas_fraction_d(0)
 End Property
 
 Public Property Get GasFraction_PumpOutlet_d() As Double
    fluidTub.calc_PVT ESP.Pdis_atma, ESP.t_dis_C
    GasFraction_PumpOutlet_d = fluidTub.gas_fraction_d(0)
 End Property
 
 
 Public Property Get c_calibr_head() As Double
    c_calibr_head = p_ESPpump.c_calibr_head
 End Property
 
 Public Property Let c_calibr_head(val As Double)
    p_ESPpump.c_calibr_head = val
 End Property
 
 Public Property Get c_calibr_power() As Double
    c_calibr_power = p_ESPpump.c_calibr_power
 End Property
  
 Public Property Get c_calibr_rate() As Double
    c_calibr_rate = p_ESPpump.c_calibr_rate
 End Property
 
