'=======================================================================================
'Unifloc 7.20  coronav                                          khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
' класс для расчета характеристик штуцера
' потребовался для организации корректного учета "крутой" характеристики штуцера
' особенность штуцера - то что при достижении критического потока через штуцер (движения потока со скоростью звука)
' давление за штуцером в определенном диапазоне перестает оказывать влияние на поток - то есть заднному дебиту
' и давление перед штуцером может соответсвовать несколько значений  давлений после (линейных)
'==============  Cchoke  ==============
' класс для расчета многофазного потока в локальном сопротивлении - штуцере
Option Explicit
Option Base 0
' геометрические параметры штуцера
Public d_up_m As Double
Public d_down_m As Double
Public d_choke_m As Double
Public t_choke_C As Double
' флюид протекающий через штуцер
Public fluid As New CPVT
Private c_calibration_ As Double
Private c_degrad_choke_ As Double                             ' choke correction factor
' кривые для текущих характеристик штуцера
' строятся для текущих параметров штуцера
Public curve As New CCurves
Private q_liqmax_m3day_ As Double  ' максимальный дебит для заданных давлений на входе и на выходе через штуцер
Private t_choke_throat_C_ As Double ' температура в штуцере
Private t_choke_av_C_
Public sonic_vel_msec As Double
' набор параметров для которых был проведен последний расчет
'Private p_pbuf_atma As Double
'Private p_plin_atma As Double
' internal vars
' параметры модели штуцера
Private k As Double '  = 0.826,'K - Discharge coefficient (optional, default  is 0.826)
Private F As Double ' = 1.25,'F - Ratio of gas spec. heat capacity at constant pressure to that at constant volume (optional, default  is 1.4)
Private c_vw As Double ' = 4176'Cvw - water specific heat capacity (J/kg K)(optional, default  is 4176)
Private a_u As Double 'upstream area
Private a_c As Double 'choke throat area
Private a_r As Double 'area ratio
Private P_r As Double  ' critical pressure for output
Private v_s As Double  ' sonic velosity
Private q_m As Double  ' mass rate
Private p_dcr As Double ' recovered downstream pressure at critical pressure ratio
Private Sub Class_Initialize()
 k = 0.826 'K - Discharge coefficient (optional, default  is 0.826)
 F = 1.25
 c_vw = 4176
 c_calibration_ = 1
 c_degrad_choke_ = 0
 
 'параметры по умолчанию
 d_up_m = 0.1
 d_down_m = 0.1
 d_choke_m = 0.01
 t_choke_C = 30
End Sub
Public Property Get d_choke_mm() As Double
    d_choke_mm = d_choke_m * 1000
End Property
Public Property Get c_calibr_fr() As Double
    c_calibr_fr = c_calibration_
End Property
'Public Property Get c_degrad_fr() As Double
'    c_degrad_fr = 1 - c_calibration_
'End Property
'
'Public Property Let c_degrad_fr(val As Double)
'    c_calibration_ = 1 - val
'End Property
Public Property Let c_calibr_fr(val As Double)
    c_calibration_ = val
End Property
Public Property Get fw_fr() As Double
    fw_fr = fluid.fw_fr ' fw_perc / 100
End Property
Public Property Get Qlmax_m3day()
    Qlmax_m3day = q_liqmax_m3day_
End Property
Public Property Get TchokeThroat_C()
    TchokeThroat_C = t_choke_throat_C_
End Property
Public Property Get TchokeAv_C()
    TchokeAv_C = t_choke_av_C_
End Property
Public Property Get PratioCrit()
    PratioCrit = P_r
End Property
Public Property Get VelSonic_msec()
    VelSonic_msec = v_s
End Property
Public Property Get Qm_kgsec()
    Qm_kgsec = q_m
End Property
Public Property Get PdownCrit_atma()
    PdownCrit_atma = p_dcr
End Property
' калибровка модели штуцера по данным измерений
Public Function calc_choke_calibration( _
            ByVal p_intake_atma As Double, _
            ByVal p_out_atma As Double, _
            t_C As Double) As Double
' p_intake_atma   - давление на входе
' p_out_atma      - давление на выходе
' t_C             - температура потока
' дебит и обводненность определяются свойствами потока
' результат коэффициент калибровки обеспечивающий согласование
' давлений и дебитов (если можно определить)
' рассчитывается напрямую как отношение расчетного дебита к фактическому
    Dim qtest As Double
    t_choke_C = t_C
    If (p_intake_atma > p_out_atma) And d_choke_m > 0 Then
        qtest = calc_choke_qliq_sm3day(p_intake_atma, p_out_atma, t_choke_C)
        c_calibration_ = fluid.qliq_sm3day / qtest
    Else
        c_calibration_ = 1
    End If
End Function
'Function calculates upstream node pressure for choke
Public Function calc_choke_p_buf(PTLine As PTtype) As PTtype
'Arguments
'PTline_atma - line pressure (downstream) ( (atma)) and temperature ( (C))
'Return upstream pressure and temperature
    
    Dim eps As Double
    Dim eps_q As Double
    eps = 0.001
    eps_q = 0.1
On Error GoTo ErrorHandler
    If (d_choke_m > d_up_m - 2 * eps) Or (d_choke_m < 0.001) Or (fluid.qliq_sm3day < eps_q) Then
        calc_choke_p_buf = PTLine
        Exit Function
    End If
    calc_choke_p_buf = calc_choke_p(PTLine, 0)
    Exit Function
ErrorHandler:
    calc_choke_p_buf = set_PT(0, 0)
    addLogMsg ("Cchoke.calc_choke_p_buf: error. set calc_choke_p_buf = 0 : p_line_atma  = " _
                 & PTLine.p_atma & "  t_choke_C = " & PTLine.t_C)
End Function
'Function calculates downstream node pressure for choke
Public Function calc_choke_p_lin(PTbuf As PTtype) As PTtype
'PTbuf - well head pressure (upstream) ( (atma)) and temperature ( (C))
'Return downstream pressure and temperature
    
    ' если расчет не возможен (решение не существует), возвращает 0, так как
    ' потенциально может возникнуть ситуация, что при заданном дебите, диаметре штуцера и
    ' получившемся давлении на входе - решения по давлению на выходе не будет существовать
    'PTbuf - well head pressure and  temperature Upstream
    
    Dim eps As Double
    Dim eps_q As Double
    eps = 0.001
    eps_q = 0.1
    If (d_choke_m > d_up_m - 2 * eps) Or (d_choke_m < 0.001) Or (fluid.qliq_sm3day < eps_q) Then
        calc_choke_p_lin = PTbuf
        Exit Function
    End If
    ' Если при расчете линейного давления возникла ошибка, то скорее всего для дебита нет соотвествия для линейного давления
On Error GoTo ErrorHandler
    calc_choke_p_lin = calc_choke_p(PTbuf, calc_p_down:=1)
    Exit Function
ErrorHandler:
    calc_choke_p_lin = set_PT(0, 0)
    addLogMsg ("Cchoke.calc_choke_plin_atma: error. set calc_choke_plin_atma = 0 : pbuf_atma  = " _
                 & PTbuf.p_atma & "  t_choke_C = " & PTbuf.t_C)
End Function
Private Function wi_calc_(P_r As Double, _
                          p_u As Double, _
                          t_av As Double, _
          Optional ByRef wi_deriv As Double)
      
    Dim Alpha As Double
    Dim Lambda As Double
    Dim betta As Double
    Dim GAMMA As Double
    Dim Delta As Double
    Dim p_av As Double
            'Calculate average pressure and temperature
            p_av = (p_u + P_r * p_u) / 2
            With fluid
                Call .calc_PVT(p_av, t_av)
                If P_r = 0 Then
                    P_r = 0.000001
                End If
                 Alpha = .rho_gas_rc_kgm3 * (.fm_oil_fr / .rho_oil_rc_kgm3 + .fm_wat_fr / .rho_wat_rc_kgm3)
                  'Calculate auxilary values
                Lambda = (.fm_gas_fr + (.fm_gas_fr * .cv_gas_JkgC + .fm_oil_fr * .cv_oil_JkgC + .fm_wat_fr * .cv_wat_JkgC) / (.cv_gas_JkgC * (.heat_capacity_ratio_gas - 1)))
                betta = .fm_gas_fr / .polytropic_exponent * P_r ^ (-1 - 1 / .polytropic_exponent)
                GAMMA = .fm_gas_fr + Alpha
                Delta = .fm_gas_fr * P_r ^ (-1 / .polytropic_exponent) + Alpha
                      
                P_r = MinReal(P_r, 1)
                wi_calc_ = 27500000# * a_c * (2 * p_u * .rho_gas_rc_kgm3 / Delta ^ 2 * (Lambda * (1 - P_r ^ (1 - 1 / .polytropic_exponent)) + Alpha * (1 - P_r)) _
                           / (1 - (a_r * GAMMA / Delta) ^ 2)) ^ (1 / 2)
                'Calculate rate derivative
                wi_deriv = (2 * Lambda * (1 - P_r ^ (1 - 1 / .polytropic_exponent)) + 2 * Alpha * (1 - P_r)) * betta - _
                    Delta * (1 - (a_r * GAMMA / Delta) ^ 2) * (Lambda * (1 - 1 / .polytropic_exponent) * P_r ^ (-1 / .polytropic_exponent) + Alpha)
            End With
End Function
Private Function wi_calc(P_r As Double, _
                         p_u As Double, _
                         t_u As Double, _
          Optional ByRef wi_deriv As Double, _
          Optional p_r_inc As Double = -1, _
          Optional ByRef d_pr As Double)
    'Auxilary properties
    Dim t_C As Double
    Dim p_av As Double
    Dim t_av As Double
    'PVT properties
    Dim n As Double
    Dim wi_deriv2 As Double
    Dim wi_2_deriv As Double
        
    'Calculate specific heat capacities
    With fluid
      Call .calc_PVT(p_u, t_u)
        
      'Calculate choke throat temperature
      t_C = (t_u + 273) * P_r ^ (1 - 1 / .polytropic_exponent) - 273
      t_choke_throat_C_ = t_C
      'Calculate average pressure and temperature
      'p_av = (p_u + P_r * p_u) / 2
      t_av = (t_u + t_C) / 2
      t_choke_av_C_ = t_av
      wi_calc = wi_calc_(P_r, p_u, t_av, wi_deriv)
      If p_r_inc > 0 Then
            P_r = P_r + p_r_inc
            Call wi_calc_(P_r, p_u, t_av, wi_deriv2)
            wi_2_deriv = (wi_deriv2 - wi_deriv) / p_r_inc
            d_pr = -wi_deriv / wi_2_deriv
      End If
    End With
End Function
Private Function wi_calc_perkins_1993(P_r As Double, p_u As Double, t_u As Double, rho_gu As Double, f_o As Double, f_g As Double, f_w As Double, _
                           Alpha As Double, Optional ByRef wi_deriv As Double, Optional ByRef n As Double)
    'Auxilary properties
    Dim t_C As Double
    Dim p_av As Double
    Dim t_av As Double
    'PVT properties
 '   Dim rs_m3m3 As Double
    Dim bo_m3m3 As Double
    Dim z As Double
    Dim bg_m3m3 As Double
    Dim bw_m3m3 As Double
    Dim c_vo As Double
    Dim c_vg As Double
    Dim Lambda As Double
    Dim betta As Double
    Dim GAMMA As Double
    Dim Delta As Double
    Dim f_pr As Double
'    Dim void As Double
    z = 1
    'Calculate specific heat capacities
    c_vo = c_vw * (0.238 + 0.302 / fluid.gamma_o + (0.0064 + 0.003 / fluid.gamma_o) * t_u)
    c_vg = z * const_r / (fluid.m_g_kgmol * (F - 1))
    'Calculate polytropic exponent
    n = (f_g * F * c_vg + f_o * c_vo + f_w * c_vw) / (f_g * c_vg + f_o * c_vo + f_w * c_vw)
    'Calculate choke throat temperature
    t_C = (t_u + 273) * P_r ^ (1 - 1 / n) - 273
    t_choke_throat_C_ = t_C
    'Calculate average pressure and temperature
    p_av = (p_u + P_r * p_u) / 2
    t_av = (t_u + t_C) / 2
    t_choke_av_C_ = t_av
    Call fluid.calc_PVT(p_av, t_av)
  '  rs_m3m3 = MinReal(fluid.rp_m3m3, rs_m3m3)
    'Recalculate specific heat capacities
    c_vo = c_vw * (0.238 + 0.302 / fluid.gamma_o + (0.0064 + 0.003 / fluid.gamma_o) * t_av)
    c_vg = z * const_r / (fluid.m_g_kgmol * (F - 1))
    'Recalculate polytropic exponent
    n = (f_g * F * c_vg + f_o * c_vo + f_w * c_vw) / (f_g * c_vg + f_o * c_vo + f_w * c_vw)
    If P_r = 0 Then
      P_r = 0.000001
    End If
    'Calculate auxilary values
    Lambda = (f_g + (f_g * c_vg + f_o * c_vo + f_w * c_vw) / (c_vg * (F - 1)))
    betta = f_g / n * P_r ^ (-1 - 1 / n)
    GAMMA = f_g + Alpha
    Delta = f_g * P_r ^ (-1 / n) + Alpha
    'Calculate isentropic mass flow rate (kg/day)
    P_r = MinReal(P_r, 1)
    wi_calc_perkins_1993 = 27500000# * a_c * (2 * p_u * rho_gu / Delta ^ 2 * (Lambda * (1 - P_r ^ (1 - 1 / n)) + Alpha * (1 - P_r)) _
                         / (1 - (a_r * GAMMA / Delta) ^ 2)) ^ (1 / 2)
    'Calculate rate derivative
    wi_deriv = (2 * Lambda * (1 - P_r ^ (1 - 1 / n)) + 2 * Alpha * (1 - P_r)) * betta - _
        Delta * (1 - (a_r * GAMMA / Delta) ^ 2) * (Lambda * (1 - 1 / n) * P_r ^ (-1 / n) + Alpha)
End Function
Public Function calc_choke_q_liqmax_m3day(ByVal p_sn As Double, _
                               ByVal t_u As Double, Optional calc_p_down As Integer = 0) As Double
    Dim Max_Ql As Double
    Dim i As Integer
    
    If (calc_p_down = 0) Then
        i = -1
        Do
            i = i + 1
        Loop Until (fluid.qliq_sm3day < 10 ^ i)
        
        If (fluid.qliq_sm3day < 0.5 * 10 ^ i) Then
            Max_Ql = 0.5 * 10 ^ i
        Else
            Max_Ql = 10 ^ i
        End If
        Max_Ql = MaxReal(50, Max_Ql)
        Max_Ql = MinReal(1500, Max_Ql)
    End If
    If (calc_p_down = 1) Then
        Max_Ql = calc_choke_qliq_sm3day(p_sn, 0, t_u)
    End If
    calc_choke_q_liqmax_m3day = Max_Ql
End Function
Public Function calc_choke_rp_max_m3m3(ByVal p_sn As Double, _
                               ByVal t_u As Double, Optional calc_p_down As Integer = 0) As Variant
' Функция находит значение максимального газового фактора при даном дебите нефти и данном давлении на входе,
' при котором давление на выходе становится нулевым
       
    Dim Max_Rp As Double
    Dim i As Integer
    Dim a As Double ' нижняя граница отрезка, в которой находится значение максимального газового фактора
    Dim b As Double ' вверхняя граница отрезка, в которой находится значение максимального газового фактора
    Dim p_d As Double
    Dim eps As Double
    eps = p_sn * 0.001
    Dim counter As Integer
    counter = 0
    Dim max_iters As Integer
    max_iters = 20
        
    If (calc_p_down = 0) Then
        i = -1
        Do
            i = i + 1
        Loop Until (fluid.rp_m3m3 < 10 ^ i)
        
        If (fluid.rp_m3m3 < 0.5 * 10 ^ i) Then
            Max_Rp = 0.5 * 10 ^ i
        Else
            Max_Rp = 10 ^ i
        End If
        Max_Rp = MaxReal(50, Max_Rp)
        Max_Rp = MinReal(800, Max_Rp)
    End If
    
    If (calc_p_down = 1) Then
        p_d = calc_choke_p(set_PT(p_sn, t_u), calc_p_down).p_atma
                                            
        If p_d < 0 Then
            calc_choke_rp_max_m3m3 = "Невозможен"
        Else
            i = 1
            Do
                i = 2 * i
                b = fluid.rp_m3m3 * i
                p_d = calc_choke_p(set_PT(p_sn, t_u), calc_p_down).p_atma
            Loop Until p_d < 0
            a = i * fluid.rp_m3m3 / 2
            
            Do
                counter = counter + 1
                Max_Rp = (a + b) / 2
                p_d = calc_choke_p(set_PT(p_sn, t_u), calc_p_down).p_atma
                If p_d > 0 Then
                    a = Max_Rp
                Else
                    b = Max_Rp
                End If
            Loop Until Abs(p_d) < eps Or counter > max_iters
        End If
    End If
    calc_choke_rp_max_m3m3 = Max_Rp
End Function
Public Function calc_choke_p(pt As PTtype, Optional calc_p_down As Integer = 0) As PTtype
    'Function calculates end node pressure for choke (weather upstream or downstream)
    Dim p_sn As Double, t_u As Double
    Dim P_en As Double
    Dim counter As Double
    Dim eps As Double
    Dim eps_p As Double
    Const max_iters As Integer = 25
    Dim void As Double
    Dim q_l As Double
    Dim P_en_min As Double
    Dim P_en_max As Double
    Dim i As Integer
    
    Dim q_good As Boolean
    Dim p_good As Boolean
    
On Error GoTo err1:
    p_sn = pt.p_atma
    t_u = pt.t_C
    counter = 0
    
    eps = fluid.qliq_sm3day * 0.0001 'set precision equal to 0.01%
    eps_p = const_pressure_tolerance
    
    If (calc_p_down = 0) Then 'Calculate upstream pressure given downstream
        'Solve for upstream pressure
        i = 1
        counter = 0
        Do
            ' ищем давление на входе заведомо превышающее необходимое для обеспечения заданного потока
            counter = counter + 1
            i = 2 * i
            P_en_max = p_sn * i
            q_l = calc_choke_qliq_sm3day(P_en_max, p_sn, t_u)
        Loop Until q_l > fluid.qliq_sm3day Or counter > max_iters
        
        If q_l <= fluid.qliq_sm3day Then   ' значит поиск дебита не увенчался успехом
             addLogMsg "calc_choke_P(calc_p_down = 0): no solution found for rate = " & Format(fluid.qliq_sm3day, "####0.00")
        End If
        
        ' определим нижнюю границу поиска давления
        P_en_min = i * p_sn / 2
        counter = 0
        Do
            ' ищем точное значение давления на входе обеспечивающего дебит
            ' потенциально можно ускорить если не делить отрезок пополам а использовать линейное приближение (характеристика должна быть довольно гладкой)
            counter = counter + 1
            P_en = (P_en_min + P_en_max) / 2
            q_l = calc_choke_qliq_sm3day(P_en, p_sn, t_u)
            If q_l > fluid.qliq_sm3day Then
                P_en_max = P_en
            Else
                P_en_min = P_en
            End If
            q_good = Abs(fluid.qliq_sm3day - q_l) < eps
            p_good = Abs(P_en_min - P_en_max) < eps_p
        Loop Until (q_good And p_good) Or counter > max_iters
        
        If (counter > max_iters) And (Abs(fluid.qliq_sm3day - q_l) > eps * 100) Then ' значит поиск дебита не увенчался успехом
            addLogMsg "calc_choke_P(calc_p_down = 0): number of iterations too much, no solution found for rate = " & Format(fluid.qliq_sm3day, "####0.00")
        End If
    End If
    Dim p_cr As Double
    If (calc_p_down = 1) Then 'Calculate downstream pressure given upstream
        'Solve for upstream pressure
        'Calculate critical oil rate
        q_l = calc_choke_qliq_sm3day(p_sn, 0, t_u)
        If isGreater(fluid.qliq_sm3day, q_l) Then 'Given oil rate can't be archieved
            P_en = -1
        Else
            If isEqual(q_l, fluid.qliq_sm3day) Then
                calc_choke_p = set_PT(0, 0)
                P_en = 0
            Else
                i = 1
                counter = 0
                Do
                    i = 2 * i
                    P_en_min = p_sn / i
                    q_l = calc_choke_qliq_sm3day(p_sn, P_en_min, t_u)
                Loop Until q_l > fluid.qliq_sm3day Or counter > max_iters
                
                If q_l <= fluid.qliq_sm3day Then   ' значит поиск дебита не увенчался успехом
                    addLogMsg "calc_choke_P(calc_p_down = 1):no solution found for rate = " & Format(fluid.qliq_sm3day, "####0.00")
                End If
                P_en_max = 2 * p_sn / i
                counter = 0
                Do
                    counter = counter + 1
                    P_en = (P_en_min + P_en_max) / 2
                    q_l = calc_choke_qliq_sm3day(p_sn, P_en, t_u)
                    If q_l > fluid.qliq_sm3day Then
                        P_en_min = P_en
                    Else
                        P_en_max = P_en
                    End If
                Loop Until Abs(fluid.qliq_sm3day - q_l) < eps Or counter > max_iters
                If counter > max_iters Then   ' значит поиск дебита не увенчался успехом
                   addLogMsg "calc_choke_P(calc_p_down = 1): number of iterations exeeded, no solution found for rate = " & Format(fluid.qliq_sm3day, "####0.00")
                End If
            End If
        End If
    End If
    calc_choke_p.p_atma = P_en
    calc_choke_p.t_C = t_u    ' пока предполагаем для штуцера температура не меняется
    
    Exit Function
err1:
    Dim strError As String
    strError = "CChoke.calc_choke_P: error"
    Call addLogMsg(strError)
    Err.Raise Err.Number, Err.source, strError & Err.Description
End Function
Private Sub init_params()
    a_u = const_Pi * d_up_m ^ 2 / 4      'upstream area
    a_c = const_Pi * d_choke_m ^ 2 / 4   'choke throat area
    a_r = a_c / a_u                     'area ratio
End Sub
Public Function calc_choke_qliq_sm3day(ByVal p_u As Double, ByVal p_d As Double, ByVal t_u As Double) As Double
    'Function calculates oil flow rate through choke given downstream and upstream pressures using Perkins correlation
    'Return ((sm3/day))
    'Arguments
    'p_u - Upstream pressure ( (atma))
    'p_d - Downstream pressure ( (atma))
    'T_u - Upstream temperature ( (C))
    
    Dim p_co As Double
    Dim min_p_d As Double
    Dim counter As Double
    Dim w_i As Double
    Dim wi_der1 As Double
    Dim d_pr As Double
    Const max_iters As Integer = 10
    Dim eps As Double
    Dim p_ri As Double
 '   Dim v_si As Double
    Dim p_dcr As Double
    Dim p_c As Double
    Dim p_ra As Double
    Dim w As Double
    Const p_r_inc As Double = 0.001
On Error GoTo err1:
    ' calc areas
    Call init_params
    
    Call fluid.calc_PVT(p_u, t_u) ' calc PVT with upstream pressure and temperature
    With fluid
        'Calculate trial output choke pressure
        p_co = p_u - (p_u - p_d) / (1 - (d_choke_m / d_down_m) ^ 1.85)
        'Solve for critical pressure ratio
        counter = 0
        If (.fm_gas_fr > 0.0000000000001) Then 'free gas present
            'Calculate specific value of error at p_ri = 0.99
            w_i = wi_calc(0.99, p_u, t_u, eps)
            eps = Abs(eps * 0.01)
            'Assume pressure ratio
            p_ri = 0.5
            Do
                'Evaluate derivative for two points to find second derivative for Newton-Raphson iteration
                w_i = wi_calc(p_ri, p_u, t_u, wi_der1, p_r_inc, d_pr)
                'limit p_ri increment to prevent crossing [0,1] boundary
                d_pr = MaxReal(-p_ri / 2, MinReal(d_pr, (1 - p_ri) / 2))
                p_ri = p_ri + d_pr
                counter = counter + 1
            Loop Until (Abs(wi_der1) < eps) Or (counter > max_iters)
            If counter > max_iters Then
               addLogMsg ("Cchoke.calc_choke_qliq_sm3day: iterations not converged. iterations number  = " & counter & "  error wi_der1 " & wi_der1 & " < " & eps)
            End If
        
            'Calculate sonic velocity of multiphase mixture (used for output)
            sonic_vel_msec = w_i / a_c * (.fm_oil_fr / .rho_oil_rc_kgm3 + .fm_wat_fr / .rho_wat_rc_kgm3 + .fm_gas_fr / .rho_gas_rc_kgm3 * p_ri ^ (-1 / .polytropic_exponent)) / 86400
        Else 'liquid flow
            p_ri = 0
            sonic_vel_msec = 5000
        End If
        
        ' calc PVT with upstream pressure and temperature
        Call fluid.calc_PVT(p_u, t_u)
        
        q_liqmax_m3day_ = k * w_i * .fm_oil_fr / .rho_oil_sckgm3 + k * w_i * .fm_wat_fr / .rho_wat_sckgm3
        q_liqmax_m3day_ = q_liqmax_m3day_ * c_calibration_
        
        'Calculate recovered downstream pressure at critical pressure ratio
        p_dcr = p_u * (p_ri * (1 - (d_choke_m / d_down_m) ^ 1.85) + (d_choke_m / d_down_m) ^ 1.85)
        'Compare trial pressure ratio with critical and assign actual pressure ratio
        'Auxilary properties
        p_c = p_ri * p_u
        p_ra = MaxReal(p_ri, p_co / p_u)
        w_i = wi_calc(p_ra, p_u, t_u, wi_der1)
        'Calculate isentropic mass flow rate
        w = k * w_i * c_calibration_
        ' calc PVT with upstream pressure and temperature
        Call fluid.calc_PVT(p_u, t_u)
        
        calc_choke_qliq_sm3day = w * .fm_oil_fr / .rho_oil_sckgm3 + w * .fm_wat_fr / .rho_wat_sckgm3
        'Asign mass flow rate
        q_m = w / 86400 '/ c_m(Units)
        'Assign output critical pressure ratio (recovered critical pressure ratio)
        P_r = p_dcr / p_u
        'convert sonic velocity
        v_s = sonic_vel_msec '/ c_l(Units)
        Exit Function
    End With
err1:
    Dim strError As String
    strError = "CChoke.calc_choke_qliq_sm3day: error"
    Call addLogMsg(strError)
    Err.Raise Err.Number, Err.source, strError & Err.Description
End Function
Public Function calc_choke_qliq_perkins_sm3day(ByVal p_u As Double, ByVal p_d As Double, ByVal t_u As Double) As Double
    'Function calculates oil flow rate through choke given downstream and upstream pressures using Perkins correlation
    'Return ((sm3/day))
    'Arguments
    'p_u - Upstream pressure ( (atma))
    'p_d - Downstream pressure ( (atma))
    'T_u - Upstream temperature ( (C))
    
    'PVT properties
    Dim r_su As Double
    Dim b_ou As Double
    Dim Z_u As Double
    Dim b_gu As Double
    Dim b_wu As Double
    Dim void As Double
    Dim m_s As Double
    Dim f_o As Double
    Dim f_w As Double
    Dim f_g As Double
    Dim rho_ou As Double
    Dim rho_wu As Double
    Dim rho_gu As Double
    'Dim v_gu As Double
    Dim Alpha As Double
    Dim p_co As Double
    Dim min_p_d As Double
    Dim counter As Double
    Dim w_i As Double
    Dim n As Double
    Dim wi_der1 As Double
    Dim wi_der2 As Double
    Dim wi_2_deriv As Double
    Dim d_pr As Double
    Const max_iters As Integer = 10
    Dim eps As Double
    Dim p_ri As Double
    Dim v_si As Double
    Dim p_dcr As Double
    Dim p_c As Double
    Dim p_ra As Double
    Dim w As Double
    Const p_r_inc As Double = 0.001
On Error GoTo err1:
    ' calc areas
    Call init_params
    ' calc PVT with upstream pressure and temperature
    Call fluid.calc_PVT(p_u, t_u)
    
    ' get nesesary PVT props at specific conditions to internal variables
    r_su = fluid.rs_m3m3   ' gas solution ration
    b_ou = fluid.bo_m3m3   ' oil FVF
    Z_u = fluid.z          ' z factor
    b_gu = fluid.bg_m3m3   ' gas FVF
    b_wu = fluid.bw_m3m3   ' water FVF
    
    'Calculate mass fractions of flowing phases
    ' correction for gas solution ratio (obsolete?)
    r_su = MinReal(fluid.rp_full_m3m3, r_su)
    ' mass for 1 liquid rate at surface
    m_s = (1 - fw_fr) * (fluid.rho_oil_sckgm3 + fluid.rp_full_m3m3 * fluid.rho_gas_sckgm3) + fw_fr * fluid.rho_wat_sckgm3
    ' mass oil fraction at specific condition (acounts for gas in oil)
    f_o = (1 - fw_fr) * (fluid.rho_oil_sckgm3 + r_su * fluid.rho_gas_sckgm3) / m_s
    ' mass gas fraction
    f_g = (1 - fw_fr) * (fluid.rp_full_m3m3 - r_su) * fluid.rho_gas_sckgm3 / m_s
    ' mass water fraction
    f_w = (fw_fr) * fluid.rho_wat_sckgm3 / m_s
    'Densities at upstream conditions
    'Gas specific volume
    rho_ou = (fluid.rho_oil_sckgm3 + r_su * fluid.rho_gas_sckgm3) / b_ou
    rho_wu = fluid.rho_wat_sckgm3 / b_wu
    rho_gu = fluid.rho_gas_sckgm3 / b_gu
    'v_gu = 1 / rho_gu
    Alpha = rho_gu * (f_o / rho_ou + f_w / rho_wu)
    'Calculate trial output choke pressure
    p_co = p_u - (p_u - p_d) / (1 - (d_choke_m / d_down_m) ^ 1.85)
    'Solve for critical pressure ratio
    counter = 0
    If (f_g > 0.0000000000001) Then 'free gas present
        'Calculate specific value of error at p_ri = 0.99
        w_i = wi_calc_perkins_1993(0.99, p_u, t_u, rho_gu, f_o, f_g, f_w, Alpha, eps, n)
        eps = Abs(eps * 0.01)
        'Assume pressure ratio
        p_ri = 0.5
        Do
            'Evaluate derivative for two points to find second derivative for Newton-Raphson iteration
            w_i = wi_calc_perkins_1993(p_ri, p_u, t_u, rho_gu, f_o, f_g, f_w, Alpha, wi_der1, n)
            w_i = wi_calc_perkins_1993(p_ri + p_r_inc, p_u, t_u, rho_gu, f_o, f_g, f_w, Alpha, wi_der2, n)
            wi_2_deriv = (wi_der2 - wi_der1) / p_r_inc
            d_pr = -wi_der1 / wi_2_deriv
            'limit p_ri increment to prevent crossing [0,1] boundary
            d_pr = MaxReal(-p_ri / 2, MinReal(d_pr, (1 - p_ri) / 2))
            p_ri = p_ri + d_pr
            counter = counter + 1
        Loop Until (Abs(wi_der1) < eps) Or (counter > max_iters)
        If counter > max_iters Then
           addLogMsg ("Cchoke.calc_choke_qliq_sm3day: iterations not converged. iterations number  = " & counter & "  error wi_der1 " & wi_der1 & " < " & eps)
        End If
    
        'Calculate sonic velocity of multiphase mixture (used for output)
        v_si = w_i / a_c * (f_o / rho_ou + f_w / rho_wu + f_g / rho_gu * p_ri ^ (-1 / n)) / 86400
    Else 'liquid flow
        p_ri = 0
        v_si = 5000
    End If
    
    ' calc PVT with upstream pressure and temperature
    Call fluid.calc_PVT(p_u, t_u)
    
    q_liqmax_m3day_ = k * w_i * f_o / fluid.rho_oil_sckgm3 + k * w_i * f_w / fluid.rho_wat_sckgm3
    q_liqmax_m3day_ = q_liqmax_m3day_ * c_calibration_
    
    'Calculate recovered downstream pressure at critical pressure ratio
    p_dcr = p_u * (p_ri * (1 - (d_choke_m / d_down_m) ^ 1.85) + (d_choke_m / d_down_m) ^ 1.85)
    'Compare trial pressure ratio with critical and assign actual pressure ratio
    'Auxilary properties
    p_c = p_ri * p_u
    p_ra = MaxReal(p_ri, p_co / p_u)
    w_i = wi_calc_perkins_1993(p_ra, p_u, t_u, rho_gu, f_o, f_g, f_w, Alpha, wi_der1, n)
    'Calculate isentropic mass flow rate
    w = k * w_i * c_calibration_
    ' calc PVT with upstream pressure and temperature
    Call fluid.calc_PVT(p_u, t_u)
    
    calc_choke_qliq_perkins_sm3day = w * f_o / fluid.rho_oil_sckgm3 + w * f_w / fluid.rho_wat_sckgm3
    'Asign mass flow rate
    q_m = w / 86400 '/ c_m(Units)
    'Assign output critical pressure ratio (recovered critical pressure ratio)
    P_r = p_dcr / p_u
    'convert sonic velocity
    v_s = v_si '/ c_l(Units)
    Exit Function
err1:
    Dim strError As String
    strError = "CChoke.calc_choke_qliq_perkins_sm3day: error"
    Call addLogMsg(strError)
    Err.Raise Err.Number, Err.source, strError & Err.Description
End Function
Public Sub build_curves()
    'надо будет построить графики для текущих показателей
    curve("pbuf_vs_Ql_curve").ClearPoints
    curve("plin_vs_Ql_curve").ClearPoints
    curve("pbuf_vs_plin_curve").ClearPoints
    
    curve("plin_vs_Ql_curve").ClearPoints
    curve("pbuf_vs_plin_curve").ClearPoints
    
    curve("pbuf_vs_Rp_curve").ClearPoints
    curve("plin_vs_Rp_curve").ClearPoints
    curve("pbuf_vs_Ql_nominal_curve").ClearPoints
    curve("plin_vs_Ql_nominal_curve").ClearPoints
    curve("pbuf_vs_Rp_nominal_curve").ClearPoints
    curve("plin_vs_Rp_nominal_curve").ClearPoints
    
    Dim kCal_temp As Double     ' временное значение для поправочного к-та
    
    Dim i As Integer
    Dim num_points As Integer
    Dim plin_Min As Double, plin_Max As Double
    Dim pbuf_Min As Double, pbuf_Max As Double
    Dim Ql_Min As Double, Ql_Max As Double
    Dim Rp_Min As Double, Rp_Max As Double
    
    ' временно тут заведем переменные - ранее они были удалены из класса
    Dim p_plin_atma As Double
    Dim p_pbuf_atma As Double
    p_plin_atma = 10
    p_pbuf_atma = 10
    kCal_temp = c_calibration_
    Ql_Min = 0
    Ql_Max = MaxReal(fluid.qliq_sm3day * 2, 100)
    Rp_Min = 50
    Rp_Max = MaxReal(fluid.rp_m3m3 * 2, 1000)
    plin_Min = 1
    plin_Max = MaxReal(p_plin_atma * 2, 100)
    pbuf_Min = 1
    pbuf_Max = MaxReal(p_pbuf_atma * 2, 100)
    num_points = const_OutputCurveNumPoints
    Dim Pbuf1 As Double, pbuf_work As Double
    Dim p_lin1 As Double, plin_work As Double
    Dim Ql1 As Double, Ql_work As Double
    Ql1 = fluid.qliq_sm3day
    Pbuf1 = p_pbuf_atma
    p_lin1 = p_plin_atma
    curve("pbuf_vs_Ql_curve").note = "условия: p_lin = " & p_lin1
    For i = 0 To num_points - 1
        ' первый график
        fluid.qliq_sm3day = i * (Ql_Max - Ql_Min) / num_points + Ql_Min
        If p_lin1 > 0 Then
            c_calibration_ = 1
            pbuf_work = calc_choke_p_buf(set_PT(p_lin1, t_choke_C)).p_atma
            curve("pbuf_vs_Ql_nominal_curve").AddPoint fluid.qliq_sm3day, pbuf_work
            c_calibration_ = kCal_temp
            pbuf_work = calc_choke_p_buf(set_PT(p_lin1, t_choke_C)).p_atma
            curve("pbuf_vs_Ql_curve").AddPoint fluid.qliq_sm3day, pbuf_work
        End If
        c_calibration_ = 1
        plin_work = calc_choke_p_lin(set_PT(Pbuf1, t_choke_C)).p_atma
        curve("plin_vs_Ql_nominal_curve").AddPoint fluid.qliq_sm3day, plin_work
        c_calibration_ = kCal_temp
        plin_work = calc_choke_p_lin(set_PT(Pbuf1, t_choke_C)).p_atma
        curve("plin_vs_Ql_curve").AddPoint fluid.qliq_sm3day, plin_work
    Next i
    Dim Rptemp As Double, Rsbtemp As Double
    Rptemp = fluid.rp_m3m3
    Rsbtemp = fluid.rsb_m3m3
    fluid.qliq_sm3day = Ql1
    For i = 0 To num_points - 1
        ' первый график
        fluid.rp_m3m3 = i * (Rp_Max - Rp_Min) / num_points + Rp_Min
        fluid.rsb_m3m3 = Rsbtemp   ' пытаемся восстановить rsb
        If p_lin1 > 0 Then
            c_calibration_ = 1
            pbuf_work = calc_choke_p_buf(set_PT(p_lin1, t_choke_C)).p_atma
            curve("pbuf_vs_Rp_nominal_curve").AddPoint fluid.rp_m3m3, pbuf_work
            c_calibration_ = kCal_temp
            pbuf_work = calc_choke_p_buf(set_PT(p_lin1, t_choke_C)).p_atma
            curve("pbuf_vs_Rp_curve").AddPoint fluid.rp_m3m3, pbuf_work
        End If
        c_calibration_ = 1
        plin_work = calc_choke_p_lin(set_PT(Pbuf1, t_choke_C)).p_atma
        curve("plin_vs_Rp_nominal_curve").AddPoint fluid.rp_m3m3, plin_work
        c_calibration_ = kCal_temp
        plin_work = calc_choke_p_lin(set_PT(Pbuf1, t_choke_C)).p_atma
        curve("plin_vs_Rp_curve").AddPoint fluid.rp_m3m3, plin_work
    Next i
    fluid.rp_m3m3 = Rptemp
    fluid.rsb_m3m3 = Rsbtemp   ' пытаемся восстановить rsb
    Const eps = 0.0001
    ' для графика зависимости линейного давления от дебита есть четкий максимум по дебиту соответсвующий критическому потоку
    ' через штуцер. Чтобы график был красивый - добавим эти точки в явном виде
    ' eps  используется так как CInterpolation  может хранить только функции
    curve("plin_vs_Ql_curve").AddPoint Qlmax_m3day - eps, Pbuf1 * PratioCrit
    curve("plin_vs_Ql_curve").AddPoint Qlmax_m3day, 0
    fluid.qliq_sm3day = Ql1
    For i = 0 To num_points - 1
        plin_work = i * (plin_Max - plin_Min) / num_points + plin_Min
        pbuf_work = calc_choke_p_buf(set_PT(plin_work, t_choke_C)).p_atma
        curve("pbuf_vs_plin_curve").AddPoint plin_work, pbuf_work
    Next i
    p_plin_atma = p_lin1
    p_pbuf_atma = Pbuf1
End Sub
 Public Function Copy(chk As Cchoke)
    d_up_m = chk.d_up_m
    d_down_m = chk.d_down_m
    d_choke_m = chk.d_choke_m
    Call fluid.Copy(chk.fluid)
    t_choke_C = chk.t_choke_C
    c_calibration_ = chk.c_calibr_fr
 End Function

