'=======================================================================================
'Unifloc 7.50 year 2024                                  khabibullinra@gmail.com
'Petroleum engineering calculations modules (macroses)
'2000 - 2023
'
'=======================================================================================
' Gaslift valve and gas line valve
Option Explicit
Public h_mes_m As Double            ' глубина установки газлифтного клапана
Public p_bellow_sc_atma As Double   ' давление зарядки сильфона на поверхности на поверхности
Public p_in_atma As Double          ' casing pressure at gas valve
Public p_v_atma As Double           ' pressure between inlet and outlet
Public p_out_atma As Double         ' tubing pressure at gas valve
Public t_C As Double             ' casing temperature at gas valve
Public q_gas_inj_scm3day As Double  ' gas rate through valve
Public crit_flow As Double
Public p_crit_atma As Double
Public q_gas_max_sm3day As Double
' IPO valve data
' for now supposed Wheatherford R-1 data
Public R As Double
Public dext_mm As Double
Public Ab_mm2 As Double
Public Ap_mm2 As Double
Public PREF As Double
Public IPO As Boolean
Private d_vkr_mm_() As Double
Private d_vkr_eff_mm_ As Double
Private d_port_mm_ As Double             ' диаметр порта
Public gamma_gas As Double ' gas properties
' задаем характеристики клапана R-1 с учетом наличия вкруток
Public Sub set_GLV_R1(Optional ByVal IPO As Boolean = False, _
                      Optional port_mm As Double = R1_port_1_8, _
                      Optional d_vkr1_mm As Double = -1, _
                      Optional d_vkr2_mm As Double = -1, _
                      Optional d_vkr3_mm As Double = -1, _
                      Optional d_vkr4_mm As Double = -1)
' set R-1 valve data
    Dim Ap_Ab As Double
    Dim i As Integer
    Me.IPO = IPO
    d_port_mm_ = port_mm
    dext_mm = 25.4
    Ab_mm2 = 200
    Select Case port_mm
        Case R1_port_1_4
            Ap_mm2 = 8.4
        Case R1_port_5_32
            Ap_mm2 = 13.55
        Case R1_port_3_16
            Ap_mm2 = 18.71
        Case R1_port_1_8
            Ap_mm2 = 33.55
        Case R1_port_5_16
            Ap_mm2 = 51.61
        Case Else
            Ap_mm2 = (port_mm ^ 2) * (1 - 0.16)  ' approximation based on R-1 table
    End Select
    Ap_Ab = Ap_mm2 / Ab_mm2
    R = Ap_Ab
    PREF = Ap_Ab / (1 - Ap_Ab)
    ' estimate effective diameter with "vkrutka"
    Dim num_vkr As Integer
    num_vkr = 0
    If d_vkr1_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve d_vkr_mm_(1 To num_vkr)
        d_vkr_mm_(num_vkr) = d_vkr1_mm
    End If
    If d_vkr2_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve d_vkr_mm_(1 To num_vkr)
        d_vkr_mm_(num_vkr) = d_vkr2_mm
    End If
    If d_vkr3_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve d_vkr_mm_(1 To num_vkr)
        d_vkr_mm_(num_vkr) = d_vkr3_mm
    End If
    If d_vkr4_mm > 0 Then
        num_vkr = num_vkr + 1
        ReDim Preserve d_vkr_mm_(1 To num_vkr)
        d_vkr_mm_(num_vkr) = d_vkr4_mm
    End If
    d_vkr_eff_mm_ = 0
    If num_vkr > 0 Then
        For i = LBound(d_vkr_mm_) To UBound(d_vkr_mm_)
            d_vkr_eff_mm_ = d_vkr_eff_mm_ + (d_vkr_mm_(i)) ^ 2
        Next i
    End If
    d_vkr_eff_mm_ = d_vkr_eff_mm_ ^ 0.5
End Sub
Public Property Get d_port_mm() As Double
    d_port_mm = d_port_mm_
End Property
Public Property Let d_port_mm(val As Double)
    d_port_mm_ = val
End Property
Public Property Get p_open_atma() As Double
    If IPO Then
        ' for opening assume p_v_atma = p_out_atma
        p_open_atma = p_bellow_rc_atma / (1 - R) - p_out_atma * R / (1 - R)
    Else
        p_open_atma = 1
    End If
End Property
Public Property Get p_bellow_rc_atma() As Double
    If IPO Then
        p_bellow_rc_atma = GLV_p_close_atma(p_bellow_sc_atma, t_C)
    Else
        p_bellow_rc_atma = 1
    End If
End Property
' функция расчета расхода газа через клапан
Public Function calc_q_gas_sm3day(Optional p_intake_atma As Double = -1, _
                             Optional p_out_atma As Double = -1, _
                             Optional t_in_C As Double = -1) As Double
    Dim rslt
    If p_intake_atma > 0 Then
        p_in_atma = p_intake_atma
    Else
        p_intake_atma = p_in_atma
    End If
    If p_out_atma > 0 Then
        Me.p_out_atma = p_out_atma
    Else
        p_out_atma = Me.p_out_atma
    End If
    If p_intake_atma < 0 Then p_intake_atma = Me.p_in_atma
    If p_out_atma < 0 Then p_out_atma = Me.p_out_atma
    If t_in_C < 0 Then t_in_C = Me.t_C
    If (p_out_atma < p_intake_atma) And (d_port_mm_ > 0) And (d_vkr_eff_mm_ = 0) Then
        rslt = GLV_q_gas_sm3day(d_port_mm_, p_intake_atma, p_out_atma, gamma_gas, t_in_C)
        calc_q_gas_sm3day = rslt(0)(0)
        p_crit_atma = rslt(0)(1)
        crit_flow = rslt(0)(2)
      '  p_v_atma = p_out_atma
    ElseIf (p_out_atma < p_intake_atma) And (d_port_mm_ > 0) And (d_vkr_eff_mm_ > 0) Then
        rslt = GLV_q_gas_vkr_sm3day(d_port_mm_, d_vkr_eff_mm_, p_intake_atma, p_out_atma, gamma_gas, t_in_C)
        calc_q_gas_sm3day = rslt(0)(0)
        p_v_atma = rslt(0)(2)
    End If
    If IPO Then
    ' need check open condition
        Dim pdif As Double
        If p_intake_atma < p_open_atma + 2 Then
            pdif = p_open_atma + 2 - p_intake_atma
            If pdif < 0 Then pdif = 0
            calc_q_gas_sm3day = calc_q_gas_sm3day * (pdif) / 2
        End If
    End If
End Function
Public Function calc_p_out_atma(p_intake_atma As Double, q_gas_scm3day As Double)
    Me.p_in_atma = p_intake_atma
    Dim res
    res = GLV_p_vkr_atma(d_port_mm, d_vkr_eff_mm_, p_intake_atma, q_gas_scm3day, gamma_gas, t_C, True)
'    ' ищем давление внутри клапана
'    If d_vkr_eff_mm_ > 0 Then
'        p_v_atma = GLV_p_atma(d_vkr_eff_mm_, p_intake_atma, q_gas_scm3day, fluid.gamma_g, t_C, True)
'    Else
'        p_v_atma = p_out_atma
'    End If
'    p_out_atma = GLV_p_atma(d_port_mm_, p_v_atma, q_gas_scm3day, fluid.gamma_g, t_C, True)
'    calc_p_out_atma = p_out_atma
'
    p_v_atma = res(0)(2)
    p_in_atma = res(0)(0)
    If IPO Then
    ' need check open condition
        Dim pdif As Double
        If p_in_atma < p_open_atma + 2 Then
            pdif = p_open_atma + 2 - p_in_atma
            If pdif < 0 Then pdif = 0
            calc_p_out_atma = calc_q_gas_sm3day * (pdif) / 2
        End If
    End If
    calc_p_out_atma = p_in_atma
End Function
' расчет давления на входе в клапан (затрубное давление)
' по давлению в НКТ
Public Function calc_p_in_atma(p_out_atma As Double, q_gas_scm3day As Double)
    Me.p_out_atma = p_out_atma
    ' ищем давление внутри клапана
    Dim res
    res = GLV_p_vkr_atma(d_port_mm, d_vkr_eff_mm_, p_out_atma, q_gas_scm3day, gamma_gas, t_C, False)
'    If d_vkr_eff_mm_ > 0 Then
'        p_v_atma = GLV_p_atma(d_vkr_eff_mm_, p_out_atma, q_gas_scm3day, fluid.gamma_g, t_C, False)
'    Else
'        p_v_atma = p_out_atma
'    End If
    p_v_atma = res(0)(2)
    p_in_atma = res(0)(0)
    calc_p_in_atma = p_in_atma
End Function
Public Function table_pin(ByVal p_in_atma As Double, ByVal t_in_C As Double) As CInterpolation
    ' calculate valve characteristics table for given d
    Dim i As Integer
    Dim q As Double
    Dim Tbl As New CInterpolation
    Dim QEps As Double
    Dim p As Double
    QEps = 0.001
    With Tbl
        q_gas_max_sm3day = Me.calc_q_gas_sm3day(p_in_atma, 1, t_in_C)
        .AddPoint 1, q_gas_max_sm3day
        .AddPoint p_crit_atma, q_gas_max_sm3day - QEps
        .AddPoint p_in_atma, 0
        Dim n As Integer
        n = 10
        For i = 1 To n - 1
            p = p_in_atma - (p_in_atma - p_crit_atma) / n * i
            q = Me.calc_q_gas_sm3day(p_in_atma, p, t_in_C)
            .AddPoint p, q
        Next i
    End With
    Set table_pin = Tbl
End Function
Private Sub Class_Initialize()
    d_vkr_eff_mm_ = 0
    IPO = False
    d_port_mm_ = 0
    h_mes_m = 0
    p_bellow_sc_atma = 0
    gamma_gas = 0.8
End Sub

