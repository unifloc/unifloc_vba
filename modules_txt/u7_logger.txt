'=======================================================================================
'Unifloc 7.24  coronav                                          khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'
' general functions providing logging capabilities in all classess and modules
'  log messages accululated in ln CLogger object and can be saved in file
'
Option Explicit
Dim lm As New CLogger           ' лог который накапливает все сообщения. поддерживает сохранение сообщений в файл
Private Function MakeLogMsg(msg, Optional msg1, Optional msg2, Optional msg3, Optional tag As String = "") As String
 Dim msg_str As String
 msg_str = Format(Date, "Short Date") & " : " & Format(Time, "Long Time") & " : " & tag & " : "
 
 If Not IsMissing(msg) Then
    msg_str = msg_str & " : " & msg
 End If
 If Not IsMissing(msg1) Then
    msg_str = msg_str & " : " & msg1
 End If
 If Not IsMissing(msg2) Then
    msg_str = msg_str & " : " & msg2
 End If
 If Not IsMissing(msg3) Then
    msg_str = msg_str & " : " & msg3
 End If
 Debug.Print msg_str                                        '   пишем в окно отладки
 MakeLogMsg = msg_str          '  сохраняем строку
End Function
Public Function addLogMsg(msg As String, Optional msg1, Optional msg2, Optional msg3)
' вывод сообщения в лог и в окно отладки
' можно задать до 3 строк, которые автоматически склеиваются в одну перед выводом
 lm.AddMsg MakeLogMsg(msg, msg1, msg2, msg3)          '  пишем в лог
End Function
Public Function getLogObject() As CLogger
  Set getLogObject = lm
End Function
Public Function CheckRanges(ByRef var_value As Double, ByVal var_name As String, ByVal var_min As Double, ByVal var_max As Double, _
                                  Optional ByVal out_comment As String = "", Optional ByVal func_name As String = "", _
                                  Optional ByVal var_set_default = False, Optional error_probability As Double = -1) As Boolean
' функция проверки диапазонов входных параметров для физ мат функци oppump
CheckRanges = False
If var_min > var_max Then
    addLogMsg ("CheckRanges:" & func_name & ": wrong check range for " & var_name & ". no check perpformed")
    Exit Function
End If
If var_value < var_min Then
    addLogMsg ("CheckRanges:" & func_name & ": variable " & var_name & " = " & var_value & " less than min value = " & var_min & ". " & out_comment)
    If var_set_default Then
        addLogMsg ("CheckRanges:" & func_name & ":for variable " & var_name & " default value set = " & var_min)
        var_value = var_min
    End If
    error_probability = increment_error_probability(error_probability)
    Exit Function
End If
If var_value > var_max Then
    addLogMsg ("CheckRanges:" & func_name & ": variable " & var_name & " = " & var_value & " greater than max value = " & var_max & ". " & out_comment)
    If var_set_default Then
        addLogMsg ("CheckRanges:" & func_name & ":для переменной " & var_name & " default value set = " & var_max)
        var_value = var_max
    End If
    error_probability = increment_error_probability(error_probability)
    Exit Function
End If
CheckRanges = True
End Function
Function increment_error_probability(ByVal er_pr As Double, Optional increment As Integer = 1)
' потенциально функция накопления ошибок будет усложняться поэтому вынесем отдельно
 If er_pr >= 0 Then
  increment_error_probability = er_pr + increment  ' накапливаем ошибки
 Else
  increment_error_probability = 1
 End If
 
End Function

