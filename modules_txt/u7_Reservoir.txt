'=======================================================================================
'Unifloc 7.9  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
' набор функций для расчета притока из пласта
'
Option Explicit
Public Function calc_Q_IPR_m3Day(ByVal Ptest As Double, ByVal Pr As Double, _
                          ByVal Pb As Double, ByVal Pi As Double, ByVal Wc As Double, Optional calc_method As Integer = 1)
'
If Ptest >= Pr Then
 addLogMsg "Предупреждение. функция calc_Q_IPR_m3Day. тестовое забойное давление " & Ptest & " больше или равно пластового " & Pr & ". дебит 0"
 calc_Q_IPR_m3Day = 0
 Exit Function
End If
Select Case calc_method
 Case 1
 calc_Q_IPR_m3Day = calc_QliqVogel_m3Day(Ptest, Pr, Pb, Pi, Wc)
End Select
End Function
Public Function calc_pwf_IPres_atma(ByVal qtest As Double, ByVal Pr As Double, _
                          ByVal Pb As Double, ByVal Pi As Double, ByVal Wc As Double, Optional calc_method As Integer = 1)
Select Case calc_method
 Case 1
 calc_pwf_IPres_atma = calc_PwfVogel_atma(qtest, Pr, Pb, Pi, Wc)
End Select
End Function
Public Function calc_pi_IPR_m3DayAtm(ByVal qtest As Double, ByVal Ptest As Double, _
                                ByVal Pr As Double, ByVal Pb As Double, ByVal Wc As Double, Optional calc_method As Integer = 1)
If Ptest >= Pr Then
 addLogMsg "Ошибка. функция calc_pi_IPR_m3DayAtm. тестовое забойное давление " & Ptest & " больше чем пластовое давление " & Pr & ". расчет продуктивности невозможен"
 calc_pi_IPR_m3DayAtm = -1
 Exit Function
End If
Select Case calc_method
 Case 1
 calc_pi_IPR_m3DayAtm = calc_PIVogel_m3DayAtm(qtest, Ptest, Pr, Pb, Wc)
End Select
End Function
'Расчет забойного давления по Вогелю с учетом поправки на обводненность
'
'
'
'
Public Function calc_QliqVogel_m3Day(ByVal P_test As Double, ByVal Pr As Double, _
                          ByVal Pb As Double, ByVal Pi As Double, ByVal Wc As Double)
 '
 ' Q_test    - дебит жидкости при котором надо определить заб. давл. м3/сут
 ' Pr        - пластовое давление, атм
 ' Pb        - давление насыщения, атм
 ' pi - коэффициент продуктивности, м3/сут/атм
 ' wc        - обводненность, %
 
 Dim qb As Double
 Dim qo_max As Double
 Dim Pwfg As Double
 Dim CG As Double
 Dim CD As Double
 Dim fw As Double
 Dim fo As Double
 
 If P_test < 0 Then
  calc_QliqVogel_m3Day = "P_test<0!"
  Exit Function
 End If
 If Pr < 0 Then
  calc_QliqVogel_m3Day = "Pr<0!"
  Exit Function
 End If
 If Pb < 0 Then
  calc_QliqVogel_m3Day = "Pb<0!"
  Exit Function
 End If
 If Pi < 0 Then
  calc_QliqVogel_m3Day = "PI<0!"
  Exit Function
 End If
 If Pr < Pb Then
  Pb = Pr
 End If
 
 ' вычисляем дебит при давлении равном давлению насыщения.
 qb = Pi * (Pr - Pb)
 If Wc > 100 Then
   Wc = 100
 End If
 If Wc < 0 Then
  Wc = 0
 End If
 
 If (Wc = 100) Or (P_test >= Pb) Then
 
   calc_QliqVogel_m3Day = Pi * (Pr - P_test)
 
 Else
   fw = Wc / 100
   fo = 1 - fw
   ' максимальный дебит чистой нефти
   qo_max = qb + (Pi * Pb) / 1.8
 '  Dim pwf_g As Double
   Pwfg = fw * (Pr - qo_max / Pi)
   
   If P_test > Pwfg Then
     Dim a As Double: Dim B As Double: Dim C As Double: Dim D As Double
     a = 1 + (P_test - (fw * Pr)) / (0.125 * fo * Pb)
     B = fw / (0.125 * fo * Pb * Pi)
     C = (2 * a * B) + 80 / (qo_max - qb)
     D = (a ^ 2) - (80 * qb / (qo_max - qb)) - 81
     If B = 0 Then
       calc_QliqVogel_m3Day = Abs(D / C)
     Else
       calc_QliqVogel_m3Day = (-C + ((C * C - 4 * B * B * D) ^ 0.5)) / (2 * B ^ 2)
     End If
     
   Else
      CG = 0.001 * qo_max
      CD = fw * (CG / Pi) + _
           fo * 0.125 * Pb * (-1 + (1 + 80 * ((0.001 * qo_max) / (qo_max - qb))) ^ 0.5)
      calc_QliqVogel_m3Day = (Pwfg - P_test) / (CD / CG) + qo_max
   End If
   
 End If
End Function
' Расчет забойного давления по Вогелю с учетом поправки на обводненность
'
'
'
'
Public Function calc_PwfVogel_atma(ByVal Q_test As Double, ByVal Pr As Double, _
                          ByVal Pb As Double, ByVal Pi As Double, ByVal Wc As Double)
 '
 ' Q_test    - дебит жидкости при котором надо определить заб. давл. м3/сут
 ' Pr        - пластовое давление, атм
 ' Pb  - давление насыщения, атм
 ' pi - коэффициент продуктивности, м3/сут/атм
 ' wc        - обводненность, %
 
 Dim qb As Double
 Dim qo_max As Double
 Dim Pwfg As Double
 Dim CG As Double
 Dim CD As Double
 Dim fw As Double
 Dim fo As Double
 
 'проверка  данных
 
 If Pr < Pb Then
  Pb = Pr
 End If
 
 If Q_test < 0 Then
  calc_PwfVogel_atma = "Q<0!"
  Exit Function
 End If
 If Pr <= 0 Then
  calc_PwfVogel_atma = "Pr<=0!"
  Exit Function
 End If
 If Pb < 0 Then
  calc_PwfVogel_atma = "Pb<0!"
  Exit Function
 End If
 If Pi <= 0 Then
  calc_PwfVogel_atma = "PI<=0!"
  Exit Function
 End If
 
 ' вычисляем дебит при давлении равном давлению насыщения.
 qb = Pi * (Pr - Pb)
 If Wc > 100 Then
   Wc = 100
 End If
 If Wc < 0 Then
  Wc = 0
 End If
 If (Wc = 100) Or (Q_test <= qb) Or (Pb = 0) Then
 
   calc_PwfVogel_atma = (Pr - Q_test / Pi)
   
 Else
   fw = Wc / 100
   fo = 1 - fw
   ' максимальный дебит чистой нефти
   qo_max = qb + (Pi * Pb) / 1.8
   
   If Q_test < qo_max Then
     
     calc_PwfVogel_atma = fw * (Pr - Q_test / Pi) + _
                 fo * 0.125 * Pb * (-1 + (1 - 80 * ((Q_test - qo_max) / (qo_max - qb))) ^ 0.5)
   Else
      CG = 0.001 * qo_max
      CD = fw * (CG / Pi) + _
           fo * 0.125 * Pb * (-1 + (1 + 80 * ((0.001 * qo_max) / (qo_max - qb))) ^ 0.5)
      calc_PwfVogel_atma = fw * (Pr - qo_max / Pi) - (Q_test - qo_max) * (CD / CG)
   End If
 End If
 If calc_PwfVogel_atma < 0 Then
       calc_PwfVogel_atma = 0
 End If
End Function
' определение продуктивности по Вогелю с коррекцией на обводненность
Public Function calc_PIVogel_m3DayAtm(ByVal Q_test As Double, ByVal P_test As Double, _
                                ByVal Pres As Double, ByVal Pb As Double, ByVal Wc As Double)
 
 Dim j As Double
 Dim Q_calibr As Double
 
 If P_test < 0 Then
  P_test = 0
  calc_PIVogel_m3DayAtm = 0
  Exit Function
 End If
  
 If Pres < Pb Then
  Pb = Pres
 End If
 
 If Q_test <= 0 Then
  calc_PIVogel_m3DayAtm = 0 ' "Q<=0!"
  addLogMsg "calc_PIVogel_m3DayAtm  ошибка Q<=0!"
  Exit Function
 End If
 If P_test <= 0 Then
  calc_PIVogel_m3DayAtm = 0
  addLogMsg "calc_PIVogel_m3DayAtm  ошибка P_test<=0!"
  Exit Function
 End If
 If Pb < 0 Then
  calc_PIVogel_m3DayAtm = 0
  addLogMsg "calc_PIVogel_m3DayAtm  ошибка Pb<0!!"
  Exit Function
 End If
 If Pres <= 0 Then
  calc_PIVogel_m3DayAtm = 0
  addLogMsg "calc_PIVogel_m3DayAtm  ошибка Pres<=0!"
  Exit Function
 End If
 ' первое приближение для Кпр считаем
 j = Q_test / (Pres - P_test)
 ' оцениваем его
 Q_calibr = calc_QliqVogel_m3Day(P_test, Pres, Pb, j, Wc)
 ' кривая нелинейная надо подбирать значение
 j = j / ((Q_calibr) / Q_test)
 Q_calibr = calc_QliqVogel_m3Day(P_test, Pres, Pb, j, Wc)
 If Abs(Q_calibr - Q_test) > 0.001 Then Debug.Assert False   ' если сработало, то алгоритм сломался
 calc_PIVogel_m3DayAtm = j
  
End Function
